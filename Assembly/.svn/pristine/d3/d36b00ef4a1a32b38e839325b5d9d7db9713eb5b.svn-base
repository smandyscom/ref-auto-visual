Imports Automation
Imports AutoNumeric
Imports System.Linq
Imports MathNet.Numerics.LinearAlgebra
Imports FA.dmCalibrationSetting
Imports FA.distanceMeter
Imports System.Xml.Serialization
Imports System.ComponentModel
Imports System.Drawing.Design

''' <summary>
''' 
''' </summary>
''' <remarks></remarks>
Public Class dmCalibrationSetting
    Inherits settingBase

    ''' <summary>
    ''' Generate shift-search route
    ''' </summary>
    ''' <remarks></remarks>
    <Editor(GetType(utilitiesUI.popupPropertyGridEditor), GetType(UITypeEditor))>
    Class edgeSearchSetting
        Implements IRoute

        <Browsable(False)>
        <XmlIgnore()>
        Property StartPoint As PositionVector
            Get
                Return __startPoint
            End Get
            Set(value As PositionVector)
                __startPoint = value
            End Set
        End Property
        ''' <summary>
        ''' In R-Frame
        ''' </summary>
        ''' <remarks></remarks>
        Dim __startPoint As PositionVector = New PositionVector(framesDefinition.R)

        Property StartOffset As Double = 0.3

        Property SearchDirection As axisEntityEnum = axisEntityEnum.X
        Property ShiftDirection As axisEntityEnum = axisEntityEnum.Y

        Property SearchDepth As Double = 3
        Property ShiftPitch As Double = 0.1
        Property ShiftCount As Integer = 10

        ''' <summary>
        ''' In mm/sec
        ''' </summary>
        ''' <value></value>
        ''' <returns></returns>
        ''' <remarks></remarks>
        Property Speed As Double = 3

        Property StartSpeed As Double = 0.1
        Property AccerlerationTime As Double = 0.05
        Property DecerlerationTime As Double = 0.05


        <XmlIgnore()>
        <Browsable(False)>
        Public ReadOnly Property MeasurePoints As List(Of PositionVector) Implements IRoute.MeasurePoints
            Get
                Dim output As List(Of PositionVector) = New List(Of PositionVector)
                Dim routeStart As PositionVector = __startPoint.Clone
                Dim routeEnd As PositionVector = __startPoint.Clone
                routeEnd.AxisValue(SearchDirection) += SearchDepth

                Dim counter As Integer = 0
                While counter <= ShiftCount
                    output.AddRange({routeStart.Clone,
                                     routeEnd.Clone})

                    routeStart.AxisValue(ShiftDirection) += ShiftPitch
                    routeEnd.AxisValue(ShiftDirection) += ShiftPitch
                    counter += 1
                End While

                Return output
            End Get
        End Property

        Friend pointCloud As List(Of Vector(Of Double)) = New List(Of Vector(Of Double))

        ''' <summary>
        ''' Feedback value , in mm
        ''' </summary>
        ''' <remarks></remarks>
        Friend startLatchX As Double = 0
        Friend endLatchX As Double = 0
        Friend startLatchY As Double = 0
        Friend endLatchY As Double = 0

    End Class


    <Browsable(False)>
    <XmlIgnore()>
    Friend ReadOnly Property ImageProcesses As Dictionary(Of framesDefinition, imageProcessSettingBlock)
        Get
            Dim output As Dictionary(Of framesDefinition, imageProcessSettingBlock) = New Dictionary(Of framesDefinition, imageProcessSettingBlock)
            output(framesDefinition.C1REAL) = C1ImageProcess
            output(framesDefinition.C2REAL) = C2ImageProcess
            output(framesDefinition.C4) = C4ImageProcess
            Return output
        End Get
    End Property
    <Browsable(False)>
   <XmlIgnore()>
    Friend ReadOnly Property RouteProcesses As Dictionary(Of framesDefinition, edgeSearchSetting)
        Get
            Dim output As Dictionary(Of framesDefinition, edgeSearchSetting) = New Dictionary(Of framesDefinition, edgeSearchSetting)
            output(framesDefinition.C1REAL) = C1RouteProcess
            output(framesDefinition.C2REAL) = C2RouteProcess
            Return output
        End Get
    End Property

    Property C1ImageProcess As imageProcessSettingBlock = New imageProcessSettingBlock
    Property C2ImageProcess As imageProcessSettingBlock = New imageProcessSettingBlock
    Property C4ImageProcess As imageProcessSettingBlock = New imageProcessSettingBlock

    Property C1RouteProcess As edgeSearchSetting = New edgeSearchSetting
    Property C2RouteProcess As edgeSearchSetting = New edgeSearchSetting

    <XmlIgnore()>
    Public Overrides Property Filename As String
        Get
            Return String.Format("{0}{1}.xml",
                                 cameraCalibrationSettingBase.settingPath,
                                 Me.ToString)
        End Get
        Set(value As String)
            'nothing to do
        End Set
    End Property

    Sub New()

    End Sub

End Class

''' <summary>
''' Procedures
''' 1. Using C1,C2,C3 Fetch edge line (Double Point)
''' 2. Using DM Cut-Measuring Edges , get N data points (3D)
''' 3. Transform these points , referenced on C1-Frame (X,Y values)
''' 4. Calculate Point-Line Distance , along Y-Axis of C1 , get N differential distance , make a standard variation as reference
''' 5. Average these value , get the Z-offset
''' </summary>
''' <remarks></remarks>
Public Class dmCalibration
    Inherits systemControlPrototype
    Implements IDisposable
    Implements IProcedure


    Public Property Arguments As Object Implements IProcedure.Arguments
    Public Property IsProcedureStarted As New flagController(Of interlockedFlag) Implements IProcedure.IsProcedureStarted

    Friend setting As dmCalibrationSetting = New dmCalibrationSetting

    Dim currentImageProcess As Dictionary(Of framesDefinition, imageProcessSettingBlock).Enumerator = Nothing
    Dim currentEdgeProcess As Dictionary(Of framesDefinition, edgeSearchSetting).Enumerator = Nothing

    Dim currentRoutePoint As List(Of PositionVector).Enumerator = Nothing

    Friend cornerByCamerasInR As Vector(Of Double) = Nothing

    ''' <summary>
    ''' Cached the image results
    ''' </summary>
    ''' <remarks></remarks>
    Dim imageResultsInReference As Dictionary(Of framesDefinition, List(Of Vector(Of Double))) =
        New Dictionary(Of framesDefinition, List(Of Vector(Of Double)))

    Enum subStatesEnum As Integer
        READY = 10
        EDGE_IMAGE = 100

        EDGE_START = 200
        ROUTE_START = 300

        DATA_PROCESS = 400
        PROCESS_DONE = 500
    End Enum

    ''' <summary>
    ''' Do Edge image processing
    ''' </summary>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Protected Function stateExecute() As Boolean
        Select Case systemSubState
            Case 0
                If IsProcedureStarted.viewFlag(interlockedFlag.POSITION_OCCUPIED) Then
                    systemSubState = subStatesEnum.READY
                End If
            Case subStatesEnum.READY
                'move carriage to chuck center
                With frames.Instance
                    .CurrentMovingItem = framesDefinition.C4
                    .CurrentRItem = itemsDefinition.CHOKE_CORNER1
                End With
                systemSubState += 10
            Case subStatesEnum.READY + 10
                If Assembly.Instance.IsAllAxesSettled Then

                    'pause to adjust light
                    Assembly.Instance.controlFlags.setFlag(assemblyArch.controlFlagsEnum.PAUSE_PRESSED)

                    currentImageProcess = setting.ImageProcesses.GetEnumerator
                    systemSubState = subStatesEnum.EDGE_IMAGE
                Else
                    '-------------------
                    '   Settling
                    '-------------------    
                End If
                '------------------------------
                '   Edge Image
                '------------------------------
            Case subStatesEnum.EDGE_IMAGE
                If currentImageProcess.MoveNext Then
                    currentImageProcess.Current.Value.onImageProcedureLoaded()
                    systemSubState += 10
                Else
                    '--------------------
                    '   All Process Done
                    '--------------------
                    'output and check
                    sendMessage(internalEnum.GENERIC_MESSAGE,
                                String.Format("C1,{0}{3}C2,{1}{3}C4,{2}",
                                              imageResultsInReference(framesDefinition.C1REAL).First.ToVectorString.Replace(vbCrLf, vbTab),
                                              imageResultsInReference(framesDefinition.C2REAL).First.ToVectorString.Replace(vbCrLf, vbTab),
                                              imageResultsInReference(framesDefinition.C4).First.ToVectorString.Replace(vbCrLf, vbTab),
                                              vbCrLf))
                    Assembly.Instance.controlFlags.setFlag(assemblyArch.controlFlagsEnum.PAUSE_PRESSED)

                    'calculated out the corner coordinate , update
                    imageResultsInReference(framesDefinition.C1REAL).First().Item(axisEntityEnum.X) = 0
                    imageResultsInReference(framesDefinition.C2REAL).First().Item(axisEntityEnum.Y) = 0
                    imageResultsInReference(framesDefinition.C4).First().Item(axisEntityEnum.Z) = 0
                    'transform measure values into LREAL frame (ideal value)
                    cornerByCamerasInR = CreateVector.Dense(Of Double)({0,
                                                                     0,
                                                                     0,
                                                                     0})
                    For Each pair As KeyValuePair(Of framesDefinition, List(Of Vector(Of Double))) In imageResultsInReference
                        cornerByCamerasInR += pair.Value.First
                    Next
                    'the average
                    cornerByCamerasInR /= 2
                    'update
                    frames.Instance.objectsDictionary(itemsDefinition.CHOKE_CORNER1).RawValue = cornerByCamerasInR

                    Dim __startPoint As PositionVector = New PositionVector(cornerByCamerasInR, framesDefinition.R)

                    With setting
                        With .C1RouteProcess
                            .StartPoint = __startPoint.Clone
                            .StartPoint.Y += .StartOffset
                            .StartPoint.X += .StartOffset

                            .StartPoint.X -= .SearchDepth / 2
                        End With
                        With .C2RouteProcess
                            .StartPoint = __startPoint.Clone
                            .StartPoint.Y += .StartOffset
                            .StartPoint.X += .StartOffset

                            .StartPoint.Y -= .SearchDepth / 2
                        End With
                    End With

                    currentEdgeProcess = setting.RouteProcesses.GetEnumerator
                    systemSubState = subStatesEnum.EDGE_START
                End If
                '-------------------
                '   Image Trigger
                '-------------------
            Case subStatesEnum.EDGE_IMAGE + 10
                currentImageProcess.Current.Value.onCameraTriggerd()
                systemSubState += 10
            Case subStatesEnum.EDGE_IMAGE + 20
                With currentImageProcess.Current
                    If .Value.IsImageProcessDone And
                        .Value.Result = Cognex.VisionPro.CogToolResultConstants.Accept Then
                        'transform to reference frame
                        Me.imageResultsInReference(.Key) = .Value.Coordinates

                        'transform to reference frame
                        Dim __collection As List(Of Vector(Of Double)) = imageResultsInReference(.Key)
                        For index = 0 To __collection.Count - 1
                            If __collection(index) IsNot Nothing Then
                                __collection(index) = (frames.Instance.Transformation(.Key, framesDefinition.R) *
                                                             New PositionVector(__collection(index), .Key)).RawValue
                            Else
                                '-----------------
                                '   Corresponding slot data inexisted
                                '-----------------
                            End If


                        Next

                        systemSubState = subStatesEnum.EDGE_IMAGE
                    ElseIf .Value.Result <> Cognex.VisionPro.CogToolResultConstants.Accept Then
                        'retry image
                        systemSubState = (subStatesEnum.EDGE_IMAGE + 10)
                    Else
                        '---------------------------
                        '   Image Processing
                        '---------------------------
                    End If
                End With
                '-----------------------------
                '   Edge Searching Start
                '-----------------------------
            Case subStatesEnum.EDGE_START
                If currentEdgeProcess.MoveNext Then
                    currentEdgeProcess.Current.Value.pointCloud.Clear()
                    currentRoutePoint = currentEdgeProcess.Current.Value.MeasurePoints.GetEnumerator
                    systemSubState = subStatesEnum.ROUTE_START
                Else
                    '---------------------
                    '   All Route Executed
                    '---------------------
                    systemSubState = subStatesEnum.DATA_PROCESS
                End If
                '--------------------------------
                '   Route Execution
                '--------------------------------
            Case subStatesEnum.ROUTE_START
                If currentRoutePoint.MoveNext Then
                    'move to start
                    With frames.Instance
                        .solveAbsAxAy(currentRoutePoint.Current, framesDefinition.LREAL)
                    End With
                    systemSubState += 10
                Else
                    '---------------------------
                    '   No Route able to execute, check another edge
                    '---------------------------
                    systemSubState = subStatesEnum.EDGE_START
                End If
            Case subStatesEnum.ROUTE_START + 10
                If Assembly.Instance.IsAllAxesSettled Then
                    systemSubState += 10
                Else
                    '---------------------
                    '   Settling
                    '---------------------
                End If
            Case subStatesEnum.ROUTE_START + 20
                If Assembly.Instance.__distanceMeter.drive(dmCommands.RT, {dmOnOff.__ON}) =
                     IDrivable.endStatus.EXECUTION_END Then
                    systemSubState += 10
                Else
                    '-------------------
                    '   Communication
                    '-------------------
                End If
            Case subStatesEnum.ROUTE_START + 30
                If Assembly.Instance.__distanceMeter.drive(dmCommands.RT, {dmOnOff.__OFF}) =
                     IDrivable.endStatus.EXECUTION_END Then

                    currentRoutePoint.MoveNext() ' 

                    For Each item As controlUnitsEnum In {controlUnitsEnum.X,
                                                          controlUnitsEnum.Y}
                        With Assembly.Instance.Profile(item)
                            .VelocityInUnit = currentEdgeProcess.Current.Value.Speed
                            .StartVelocityInUnit = currentEdgeProcess.Current.Value.StartSpeed
                            .AccelerationTime = currentEdgeProcess.Current.Value.AccerlerationTime
                            .DecelerationTime = currentEdgeProcess.Current.Value.DecerlerationTime
                        End With
                    Next



                    frames.Instance.solveAbsAxAy(currentRoutePoint.Current, framesDefinition.LREAL)
                    systemSubState += 10

                Else
                    '-------------------
                    '   Communication
                    '-------------------
                End If
            Case subStatesEnum.ROUTE_START + 40
                If Assembly.Instance.IsAllAxesSettled Then

                    Assembly.Instance.Profile(controlUnitsEnum.X).VelocityInUnit = pData.MotorPoints(motorPoints.MsX_ZERO).VelocityInUnit
                    Assembly.Instance.Profile(controlUnitsEnum.Y).VelocityInUnit = pData.MotorPoints(motorPoints.MsY_ZERO).VelocityInUnit

                    systemSubState += 10
                Else
                    '-------------------
                    '   Settling
                    '-------------------
                End If
            Case subStatesEnum.ROUTE_START + 50
                If Assembly.Instance.__distanceMeter.drive(dmCommands.MS, {dmTaskEnum.TASK_EDGE_AUTO_PEAK}) =
                     IDrivable.endStatus.EXECUTION_END Then
                    systemSubState += 10
                Else
                    '-------------------
                    '   Communication
                    '-------------------
                End If
            Case subStatesEnum.ROUTE_START + 60
                With Assembly.Instance

                    If .xMotorControl.IsLatched And
                        .yMotorControl.IsLatched And
                      Assembly.Instance.__distanceMeter.IsMeasureValueAvailable Then

                        'the measure point in LREAL-Frame
                        Dim measuredPointInLreal As PositionVector = New PositionVector(framesDefinition.LREAL) With {.Z =
                            Assembly.Instance.__distanceMeter.MeasureValue}
                        Dim transformationLRealToR As htmEdgeElementary = frames.Instance.ForwardKinematic(.xMotorControl.LatchedValue,
                                                                                                           .yMotorControl.LatchedValue,
                                                                                                           0,
                                                                                                           framesDefinition.LREAL)
                        'calculating measure point in R
                        Dim measuredPointInR As PositionVector = transformationLRealToR *
                                                                                                    measuredPointInLreal

                        currentEdgeProcess.Current.Value.pointCloud.Add(measuredPointInR.RawValue.SubVector(0, 3))
                    Else
                        '--------------------
                        '   Not scanned the edge point
                        '--------------------
                    End If

                    systemSubState = subStatesEnum.ROUTE_START

                End With
                '-------------------------------------------
                '   All Data Fetched
                '-------------------------------------------
            Case subStatesEnum.DATA_PROCESS
                'calculated out peak position by distance meter
                'In LREAL Frame (real value)
                Dim transformationRtoLReal = frames.Instance.Transformation(framesDefinition.R,
                                                                        framesDefinition.LREAL)

                'real
                Dim cornerByDistanceMeterInLReal As Vector(Of Double) = (transformationRtoLReal *
                                                                New PositionVector(AutoNumeric.fittingMethods.line3DIntersection(setting.RouteProcesses(framesDefinition.C1REAL).pointCloud,
                                                                                                                                 setting.RouteProcesses(framesDefinition.C2REAL).pointCloud), framesDefinition.R)).RawValue
                'calculated out peak position by cameras (R2)
                'In LREAL Frame
                'ideal
                Dim cornerByCamerasInLreal As Vector(Of Double) = (transformationRtoLReal *
                                                                 New PositionVector(cornerByCamerasInR, framesDefinition.R)).RawValue

                'calculate origin offset in L-Real frame
                Dim originError As PositionVector = New PositionVector(cornerByDistanceMeterInLReal - cornerByCamerasInLreal, framesDefinition.LREAL)

                sendMessage(internalEnum.GENERIC_MESSAGE, String.Format("Origin Error,Camera,Distance Meter,{3}{0}{3}{1}{3}{2}",
                                                                        originError.RawValue.ToVectorString.Replace(vbCrLf, vbTab),
                                                                        cornerByCamerasInLreal.ToVectorString.Replace(vbCrLf, vbTab),
                                                                        cornerByDistanceMeterInLReal.ToVectorString.Replace(vbCrLf, vbTab),
                                                                        vbCrLf))

                frames.Instance.Elementray(framesDefinition.LREAL, framesDefinition.L).Origin -= originError

                systemSubState = subStatesEnum.PROCESS_DONE

            Case subStatesEnum.PROCESS_DONE
                IsProcedureStarted.resetFlag(interlockedFlag.POSITION_OCCUPIED)
                systemSubState = 0
        End Select

        Return 0

    End Function

    Protected Sub New()
        With Me.setting
            .Load(Nothing)

        End With
        Me.systemMainStateFunctions(systemStatesEnum.EXECUTE) = AddressOf stateExecute
        Me.systemMainState = systemStatesEnum.EXECUTE
    End Sub

#Region "singleton interface"
    ''' <summary>
    ''' Singalton pattern
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Shared ReadOnly Property Instance As dmCalibration
        Get
            If __instance Is Nothing Then
                __instance = New dmCalibration
            End If
            Return __instance
        End Get
    End Property
    Shared __instance As dmCalibration = Nothing
#End Region

#Region "IDisposable Support"
    Private disposedValue As Boolean ' 偵測多餘的呼叫

    ' IDisposable
    Protected Overridable Sub Dispose(disposing As Boolean)
        If Not Me.disposedValue Then
            If disposing Then
                ' TODO:  處置 Managed 狀態 (Managed 物件)。
            End If

            setting.Save()
        End If
        Me.disposedValue = True
    End Sub

    Protected Overrides Sub Finalize()
        ' 請勿變更此程式碼。在上面的 Dispose(ByVal disposing As Boolean) 中輸入清除程式碼。
        Dispose(False)
        MyBase.Finalize()
    End Sub

    ' 由 Visual Basic 新增此程式碼以正確實作可處置的模式。
    Public Sub Dispose() Implements IDisposable.Dispose
        ' 請勿變更此程式碼。在以上的 Dispose 置入清除程式碼 (視為布林值處置)。
        Dispose(True)
        GC.SuppressFinalize(Me)
    End Sub
#End Region

End Class


