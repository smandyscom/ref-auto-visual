Imports AutoNumeric

Public Class c4CalibrationSetting
    Inherits cameraCalibrationSettingRectangle

    ''' <summary>
    ''' 
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Property C3ImageProcessSetting As imageProcessSettingBlock = New imageProcessSettingBlock

    Property BallPosition As PositionVector = New PositionVector(framesDefinition.R)

    ''' <summary>
    ''' Do ball position offset
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Public Overrides ReadOnly Property MeasurePoints As List(Of PositionVector)
        Get
            Dim __measurePoints As List(Of PositionVector) = MyBase.MeasurePoints

            __measurePoints.ForEach(Sub(__point As PositionVector)
                                        With __point
                                            .X += BallPosition.X
                                            .Y += BallPosition.Y
                                        End With
                                    End Sub)

            Return __measurePoints
        End Get
    End Property

    Sub New()
        MyBase.New(framesDefinition.Y0REAL,
                   framesDefinition.Y0)
    End Sub

End Class
''' <summary>
''' 
''' </summary>
''' <remarks></remarks>
Public Class c4Calibration
    Inherits cameraCalibrationBase

    ''' <summary>
    ''' As local origin
    ''' </summary>
    ''' <remarks></remarks>
    Private Property BallPosition As PositionVector
        Get
            Return CType(Me.CurrentTarget, c4CalibrationSetting).BallPosition
        End Get
        Set(value As PositionVector)
            CType(Me.CurrentTarget, c4CalibrationSetting).BallPosition = value
        End Set
    End Property

    Dim WithEvents c3TriggerHandle As imageProcessTriggerEventArgs = Nothing

    Protected Friend Overrides Sub moveAction()
        'move current position under views of C4
        frames.Instance.solveAbsAxAy(nominalPosition.Current, framesDefinition.C4)
    End Sub

    Public Overrides Sub settingSaveAction()
        CurrentTarget.Save()
    End Sub

    ''' <summary>
    ''' In order to sort out Y0REAL , 
    ''' turns nominal points on Y0
    ''' turns real point on Y0REAL
    ''' </summary>
    ''' <remarks></remarks>
    Protected Friend Overrides Function postHandlingAction(nominalPosition As PositionVector, measuredPosition As PositionVector) As measuredDataPair
        With frames.Instance
            Dim real As PositionVector = .Transformation(framesDefinition.R, framesDefinition.Y0REAL) * BallPosition
            Dim nominal As PositionVector = .Transformation(framesDefinition.C4, framesDefinition.Y0REAL) * New PositionVector(measuredPosition.RawValue, framesDefinition.C4)
            Return New measuredDataPair(nominal.RawValue,
                                        real.RawValue,
                                         utilities.selectionEnums.X Or utilities.selectionEnums.Y)
        End With
    End Function

    Protected Overrides Function preparationProcedure(ByRef state As Integer) As Boolean
        '-------------------------
        '   Take Real Ball Position On FOV of C3
        '-------------------------
        Select Case state
            Case 0
                With frames.Instance
                    .CurrentMovingItem = framesDefinition.C4
                    .CurrentRItem = itemsDefinition.C3_ORIGIN
                End With
                state = 10
            Case 10
                If Assembly.Instance.IsAllAxesSettled Then
                    c3TriggerHandle = New imageProcessTriggerEventArgs(CurrentTarget.ImageProcessSetting.ToolBlock)
                    onCameraTriggerd(c3TriggerHandle)
                    state = 100
                Else
                    '------------------
                    '   Axes Moving
                    '------------------
                End If
            Case 100
                '--------------
                '   Finished when camera worked 
                '--------------
                Return Not __isCameraTriggerd
        End Select

        Return False
    End Function

#Region "singleton interface"
    ''' <summary>
    ''' Singalton pattern
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Shared ReadOnly Property Instance As c4Calibration
        Get
            If __instance Is Nothing Then
                __instance = New c4Calibration
            End If
            Return __instance
        End Get
    End Property
    Shared __instance As c4Calibration = Nothing
#End Region

    Protected Sub New()
        '------------------------
        '   Loading Setting Files
        '------------------------
        CurrentTarget = New c4CalibrationSetting()
        CurrentTarget.Load(Nothing)

        BallPosition = frames.Instance.objectsDictionary(itemsDefinition.C3_ORIGIN)
    End Sub


    Protected Overrides Sub imageProcessDone(sender As Object, e As imageProcessEndEventArgs) Handles c3TriggerHandle.ImageProcessDone
        '------------------------------------
        '   Calculating out the real position
        '------------------------------------
        BallPosition = frames.Instance.Transformation(framesDefinition.C3REAL, framesDefinition.R) *
            New PositionVector(e.OutputPositionInUnit, framesDefinition.C3REAL)

        MyBase.imageProcessDone(sender, e)
    End Sub


End Class
