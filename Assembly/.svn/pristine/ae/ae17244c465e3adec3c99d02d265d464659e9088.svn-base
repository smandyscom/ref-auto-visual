Imports Automation
Imports Automation.Components.Services
Imports AutoNumeric

Public Class userControlMainPanel

    WriteOnly Property AssemblyReference As Assembly
        Set(value As Assembly)
            __assemblyReference = value
            pauseBlockReference = value.PauseBlock
            messengerReference = value.CentralMessenger
            BasicLoadMainPanel(Me, EventArgs.Empty)
        End Set
    End Property

    Dim confirmForm As formConfirm = New formConfirm

    Dim WithEvents __assemblyReference As Assembly = Nothing
    Dim WithEvents pauseBlockReference As interceptObject = Nothing
    Dim WithEvents messengerReference As messageHandler = Nothing

    Dim gripperControls As List(Of Button) = New List(Of Button)

    Property MaxLines As Integer = 8

    Private Sub BasicLoadMainPanel(sender As Object, e As EventArgs) Handles MyBase.Load
        If __assemblyReference Is Nothing Or
            TimerRefresh.Enabled Then
            Exit Sub
        End If

        ButtonShutdown.Enabled = True
        enableInitializingButton(sender, e)

        'message bus initialize
        With UserControlMessageMainPanel
            .messengerReference = __assemblyReference.CentralMessenger
            .IsValidToShow = userControlMessage.generateMessageFilters({AddressOf .isNonRedundantMessage,
                                                                        AddressOf Me.isAlarmOrStatusMessage})
            .MessageFormator = Function(__sender As messageHandler, __e As messagePackageEventArg) (String.Format(vbCrLf & "{0} {1}", __e.Message.TimeStamp, __e.Message.AdditionalInfo))
        End With

        '---------------------------
        '   Gripper Control
        '---------------------------
        ButtonGRIPVAC.Tag = outputAddress.GRIP_VAC
        ButtonGRIPOPEN.Tag = outputAddress.GRIP_OPEN
        ButtonDisp.Tag = outputAddress.DISP
        ButtonPdEnable.Tag = outputAddress.PD_EN
        ButtonLdr1.Tag = outputAddress.LSR_DR1_DIS
        ButtonLdr2.Tag = outputAddress.LSR_DR2_DIS

        gripperControls.AddRange({ButtonGRIPVAC,
                                  ButtonGRIPOPEN,
                                  ButtonDisp,
                                  ButtonPdEnable,
                                  ButtonLdr1,
                                  ButtonLdr2})
        gripperControls.ForEach(Sub(__button As Button) __button.Text = __button.Tag.ToString)

        With DataGridViewErrorMatrix
            .DataSource = frames.Instance.ErrorMatrixs
            .AutoSizeColumnsMode = DataGridViewAutoSizeColumnsMode.DisplayedCells
            For Each item As DataGridViewColumn In .Columns
                item.Visible = False
            Next
            'except these column...
            For Each item As String In {"Name",
                                        "LastUpdateTime",
                                        "ControlVectorString"}
                .Columns(item).Visible = True
            Next
            .Columns("Name").DisplayIndex = 0 'move to very first index
            For Each item As eulerHtmTR In frames.Instance.ErrorMatrixs
                AddHandler item.ErrorContentUpdated, AddressOf updateErrorContent
            Next
        End With
        With TableLayoutPanelAnalog.Controls
            .Clear()
            Assembly.Instance.analogChannels.ForEach(Sub(__item As [Enum])
                                                         Dim __monitor = New userControlAnalogMonitor
                                                         __monitor.Tag = __item
                                                         .Add(__monitor)
                                                     End Sub)
        End With
        With TableLayoutPanelBondMaterial.Controls
            .Clear()
            For index = 0 To Assembly.Instance.BondedMaterialData.Count - 1
                .Add(New userControlMaterial With {.Data = Assembly.Instance.BondedMaterialData(index)})
            Next

        End With

        TimerRefresh.Enabled = True
    End Sub
    Function isAlarmOrStatusMessage(sender As messageHandler, e As messagePackageEventArg) As Boolean
        Return e.Message.PrimaryKey.Equals(GetType(alarmGeneric)) Or e.Message.PrimaryKey.Equals(statusEnum.GENERIC_MESSAGE)
    End Function
    Sub pauseHandler() Handles pauseBlockReference.InterceptedEvent
        Me.Invoke(Sub() ButtonPause.BackColor = Color.Yellow)
    End Sub
    Sub unpauseHandler() Handles pauseBlockReference.UninterceptedEvent
        Me.Invoke(Sub() ButtonPause.BackColor = DefaultBackColor)
    End Sub


    Private Sub systemShutDownHandler(sender As Object, e As EventArgs) Handles __assemblyReference.SystemClosed
        Me.Invoke(Sub()

                      Try

                          confirmForm = New formConfirm()
                          confirmForm.DialogResult = DialogResult.Cancel
                          confirmForm.Message = String.Format("System Shudown , Reason : ({0:G})", e.ToString())
                          confirmForm.ButtonCancel.Enabled = False
                          If (confirmForm.ShowDialog() = DialogResult.OK) Then
                              My.Application.ApplicationContext.MainForm.Close()
                              End
                          End If

                      Catch ex As Exception

                          Throw New Exception("systemShutDownHandler(sender As Object, e As closeEvent)" + ex.Message, ex)

                      End Try

                  End Sub)
    End Sub

    Private flipStatus As Boolean = False
    Private Sub flashButton(sender As Object, e As EventArgs) Handles TimerFlash.Tick
        utilitiesUI.controlFollowBooleanColor(ButtonStart, flipStatus, Color.LimeGreen, SystemColors.Control)
        flipStatus = Not flipStatus
    End Sub
    '-------------------------------------
    '   Initializing button routine
    '-------------------------------------
    Public Sub enableInitializingButton(sender As Object, e As EventArgs)
        '--------
        '   Once start pressed to ingite
        '--------
        Me.Invoke(Sub()
                      ButtonStart.Enabled = True
                      TimerFlash.Enabled = True
                  End Sub)
    End Sub

    Private Sub BasicButtonClick(sender As Object, e As EventArgs) Handles ButtonShutdown.Click,
        ButtonPause.Click,
        ButtonStart.Click,
        ButtonFinish.Click,
        ButtonPointTeach.Click

        Select Case sender.Name
            '-------------
            '   System Part
            '-------------
            Case ButtonStart.Name
                ButtonStart.Enabled = False
                ButtonStart.BackColor = Color.LimeGreen
                TimerFlash.Enabled = False
                __assemblyReference.controlFlags.setFlag(assemblyArch.controlFlagsEnum.ABLE_IGNITE)
                __assemblyReference.operationSignals.setFlag(operationSignalsEnum.__START)
                '---------------------------------------------------------------------------------------
                'todo , setup ignite flag
                'i.e    'systemReference.unloaderMainControl.controlFlags.setFlag(unloaderSystemControl.controlFlagsEnum.ABLE_IGNITE)
                '---------------------------------------------------------------------------------------
            Case ButtonShutdown.Name
                confirmForm.DialogResult = DialogResult.Cancel
                confirmForm.Message = "Do You Really Want To Shutdown System ?"
                If (confirmForm.ShowDialog() = DialogResult.OK) Then
                    __assemblyReference.controlFlags.setFlag(assemblyArch.controlFlagsEnum.IS_ABORT_SYSTEM)
                End If
            Case ButtonPause.Name
                __assemblyReference.controlFlags.setFlag(assemblyArch.controlFlagsEnum.PAUSE_PRESSED)
            Case ButtonFinish.Name
                Dim confirmForm As formConfirm = New formConfirm With {.DialogResult = DialogResult.Cancel,
                                    .Message = "Confirm to finish auto run?", .StartPosition = FormStartPosition.CenterScreen}
                If (confirmForm.ShowDialog = DialogResult.OK) Then
                    __assemblyReference.operationSignals.setFlag(operationSignalsEnum.__STOP)
                End If
            Case ButtonPointTeach.Name
                Dim __login As formPassword = New formPassword With {.StartPosition = FormStartPosition.CenterScreen}

                If (__login.ShowDialog = DialogResult.OK) Then
                    'rising motor setting form
                    Dim _formSetting As formSetting = New formSetting()
                    _formSetting.ShowDialog()
                    _formSetting.Dispose()
                Else
                    'cancel or incorrect , Hsien , 2015.06.16
                End If
        End Select
    End Sub

    Sub gripperButtonClick(sender As Button, e As EventArgs) Handles ButtonGRIPVAC.Click,
        ButtonGRIPOPEN.Click,
        ButtonDisp.Click,
        ButtonPdEnable.Click,
        ButtonLdr1.Click,
        ButtonLdr2.Click

        'flip
        mainIOHardware.writeBit(sender.Tag,
                                Not mainIOHardware.readBit(sender.Tag))

    End Sub


    Private Sub timerRefreshTick(sender As Object, e As EventArgs) Handles TimerRefresh.Tick
        gripperControls.ForEach(Sub(__button As Button)
                                    utilitiesUI.controlFollowBooleanColor(__button, mainIOHardware.readBit(__button.Tag),
                                                                          Color.Green,
                                                                          Control.DefaultBackColor)
                                End Sub)

    End Sub
    ''' <summary>
    ''' 
    ''' </summary>
    ''' <remarks></remarks>
    Sub updateErrorContent()
        Me.Invoke(Sub()
                      With DataGridViewErrorMatrix
                          .Update()
                          .Refresh()
                      End With
                  End Sub)
    End Sub

End Class
