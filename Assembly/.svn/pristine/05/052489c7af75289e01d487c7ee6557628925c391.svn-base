Imports Automation
Imports MathNet.Numerics.LinearAlgebra
Imports AutoNumeric
Imports System.Xml.Serialization

''' <summary>
''' 
''' </summary>
''' <remarks></remarks>
Public Class rectangleSearchRouteSetting
    Inherits settingBase
    Implements IRoute

    ''' <summary>
    ''' The center position/pose
    ''' </summary>
    ''' <remarks></remarks>
    <XmlIgnore()>
    Property Start As htmEdgeElementary = Nothing

    ''' <summary>
    ''' In mm
    ''' </summary>
    ''' <remarks></remarks>
    Property RangeX As Double = 0.01
    Property RangeY As Double = 0.01
    Property StepX As Double = 0.0005
    Property StepY As Double = 0.0005

    Property Height As Double = 1

    Dim it As List(Of PositionVector).Enumerator = Nothing

    ''' <summary>
    ''' Reset Iterator
    ''' </summary>
    ''' <remarks></remarks>
    Sub reset()
        it = MeasurePoints.GetEnumerator
    End Sub

    ''' <summary>
    ''' 
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    <XmlIgnore()>
    ReadOnly Property NextTransformation As htmEdgeElementary
        Get
            If it.MoveNext Then

                Dim __nextTransforamtion As htmEdgeElementary = Start.Clone
                __nextTransforamtion.Origin += it.Current
                Return __nextTransforamtion

            Else
                Return Nothing
            End If
        End Get
    End Property
    <XmlIgnore()>
    Public ReadOnly Property MeasurePoints As List(Of PositionVector) Implements IRoute.MeasurePoints
        Get

            Dim leftUpCorner As PositionVector = New PositionVector(Start.From) With {.X = -RangeX / 2,
                                                                                                    .Y = -RangeY / 2}

            Dim output As List(Of PositionVector) = New List(Of PositionVector)
            Dim xAccumulation As Double = 0
            Dim yAccumulation As Double = 0

            While yAccumulation <= RangeY
                While xAccumulation <= RangeX
                    Dim __nextPoint As PositionVector = leftUpCorner.Clone
                    With __nextPoint
                        .X += xAccumulation
                        .Y += yAccumulation
                        .Z = Height
                    End With
                    output.Add(__nextPoint)
                    xAccumulation += StepX
                End While
                xAccumulation = 0 'reset
                yAccumulation += StepY
            End While

            Return output
        End Get
    End Property

End Class




''' <summary>
''' 1. move X-stage to ready position (DIE_REAL)
''' 2. let LPC_REAL align to DIE_REAL_DRY at certain height
''' 3. xy-stage searching
''' 4. output:
'''  i  .peak value in the searching region
'''  ii. right/left beam width
'''  iii. right/left beam center 
''' 
''' 
'''after searching
'''inline requirment , the peak value should over 0.75V
'''if passed , do searching
'''after searching , do data fitting (parabolic) along X,Y , then you can get
'''left peak XY
'''right peak XY
'''do dry alignment , align peak XY and yaw
''' 
''' All X-Y coordinate referenced to DIE coordinate
''' </summary>
''' <remarks></remarks>
Public Class energySearch
    Inherits systemControlPrototype
    Implements IProcedure

    Friend setting As rectangleSearchRouteSetting = New rectangleSearchRouteSetting

    Public Property Result As IProcedure.procedureResultEnums Implements IProcedure.Result
    ''' <summary>
    ''' Arguments
    ''' </summary>
    ''' <value></value>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Public Property Arguments As Object Implements IProcedure.Arguments
        Get
            Return __materialReference
        End Get
        Set(value As Object)
            __materialReference = value
        End Set
    End Property
    Public Property IsProcedureStarted As New flagController(Of interlockedFlag) Implements IProcedure.IsProcedureStarted

    Dim __materialReference As materialData = Nothing

    Dim leftRawData As List(Of Vector(Of Double)) = New List(Of Vector(Of Double))
    Dim rightRawData As List(Of Vector(Of Double)) = New List(Of Vector(Of Double))

    Friend dieCoordinate As htmEdgeElementary = Nothing

    Friend lpcDieTransformation As htmEdgeElementary = Nothing
    Friend nextTransformation As htmEdgeElementary = Nothing
    ''' <summary>
    ''' 0: Origin/Pose had been aligned in some height
    ''' </summary>
    ''' <returns></returns>
    ''' <remarks></remarks>
    Function stateExecute() As Integer

        Select Case systemSubState
            Case 0
                If IsProcedureStarted.viewFlag(interlockedFlag.POSITION_OCCUPIED) Then
                    'let all axis perfectly aligned
                    'x,y coincidence , z at first stage height

                    'enable pd , start scanning
                    mainIOHardware.writeBit(outputAddress.PD_EN, True)
                    setting.reset()

                    lpcDieTransformation.Origin = New PositionVector(Nothing) With {.Z = setting.Height}

                    frames.Instance.solveS(lpcDieTransformation)
                    systemSubState += 10
                Else
                    '---------------------
                    '
                    '---------------------
                End If
            Case 10
                If Assembly.Instance.CommandEndStatus(controlUnitsEnum.S) =
                     IDrivable.endStatus.EXECUTION_END Then
                    'TODO , read initial value,log only

                    'setup search center
                    setting.Start = lpcDieTransformation
                    systemSubState += 10
                Else
                    '---------------------------
                    '   
                    '---------------------------
                End If
            Case 20
                nextTransformation = setting.NextTransformation
                If nextTransformation IsNot Nothing Then
                    frames.Instance.solveS(nextTransformation)
                    systemSubState += 10
                Else
                    '----------------------
                    '   Search finished
                    '----------------------
                    systemSubState = 500
                End If
            Case 30
                If Assembly.Instance.CommandEndStatus(controlUnitsEnum.S) =
                     IDrivable.endStatus.EXECUTION_END Then

                    leftRawData.Add(CreateVector.Dense(Of Double)({nextTransformation.Origin.X,
                                                                   nextTransformation.Origin.Y,
                                                                   mainIOHardware.readDouble(inputAddress.PD_LEFT)}))
                    rightRawData.Add(CreateVector.Dense(Of Double)({nextTransformation.Origin.X,
                                                                   nextTransformation.Origin.Y,
                                                                   mainIOHardware.readDouble(inputAddress.PD_RIGHT)}))
                    systemSubState = 20
                Else
                    '-----------------------
                    '   Settling
                    '-----------------------
                End If
            Case 500
                '-----------------
                '   Data Handling
                '-----------------
                __materialReference.scanningDatas(Me.dieCoordinate.From) =
                    New scanningData(leftRawData,
                                     rightRawData)

                'revised the coordinate system
                dieCoordinate.RawValue = __materialReference.scanningDatas(Me.dieCoordinate.From).RevicedCoordinate

                Result = IProcedure.procedureResultEnums.SUCCESS
                IsProcedureStarted.resetFlag(interlockedFlag.POSITION_OCCUPIED)
                systemSubState = 0

        End Select

        Return 0
    End Function

    Public Sub New(__dieCoordinate As htmEdgeElementary)
        dieCoordinate = __dieCoordinate

        Me.systemMainStateFunctions(systemStatesEnum.EXECUTE) = AddressOf stateExecute
        systemMainState = systemStatesEnum.EXECUTE

        Me.setting.Load(String.Format("{0}{1}.xml",
                                      My.Application.Info.DirectoryPath & "\Data\calibration\",
                                      Me.dieCoordinate.From.ToString))

    End Sub

End Class
