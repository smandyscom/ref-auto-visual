Imports Automation.Components.Services
Imports System.ComponentModel
Imports Automation.Components.CommandStateMachine
Imports System.Threading

Namespace Components
    Namespace CommandStateMachine

        Public Class alarmContentMotor : Inherits alarmContextBase



            Sub New()
                Me.PossibleResponse = responseWays.ABORT
            End Sub

            Shared Property MotorEnumType As Type = Nothing 'Hsien , 2015.06.18

            Public Overrides Function ToString() As String
                ' append the basic string
                Dim motor As motorControl = CType(Me.Sender, motorControl)
                Return MyBase.ToString() + vbCrLf +
                    String.Format("({0}):", motor.MotorIndex) +
                String.Format("[{0}][{1}][{2}]", motor.ReturnError, motor.MotionStatus, motor.ErrorStatus)
            End Function
        End Class

        Public Class motorControl
            Inherits driveBase

#Region "Definitions"
            Public Enum motorCommandEnum As Integer
                NONE = 0                  ' no command in execute
                WAIT_RECALL
                '----------------------------
                GO_HOME
                GO_POSITION
                STOP_FORCELY            ' stop motion immeditatly
                STOP_SLOW_DOWN          ' stop motion in slope
                '---------------------------
                JOG
                GO_POSITION_OPEN_LOOP
                '---------------------------
                WAIT_MOTION_STOP        'Hsien , 2015.01.23 ,   wait hardware stopped signal
                MOTION_PAUSE
                MOTION_RESUME
                '---------------------------
                V_CHANGE            ' Hsien , 2014.09.26
                '---------------------------
                GO_POSITION_COMBINED    ' Hsien , 2014.09.27
                '----------------------------
                'START_MOVE  'master mode
                'START_MOVE_SLAVE    'slave mode
                '----------------------------
                SYNCHRON_MASTER 'master mode
            End Enum
            Public Enum statusEnum
                ' the internal status 
                ' also the return value to caller of drive()
                'NONE
                EXECUTING = &H0                 ' 0x00
                EXECUTION_END = &H1000            ' 0x1000
                EXECUTION_END_FAIL = &H1100       ' 0x1100 , with sub-code
            End Enum
            Public Enum motionSubStateEnum
                SETUP_AND_GO
                WAITEXECUTION
                WAITFINEPOSITION
            End Enum

            Public Enum mainFailTypeEnum
                AMAX_ERROR = &H1000000
                DEVICE_ERROR = &H2000000
            End Enum
            Public Enum sencondFailTypeEnum
                ERROR_STOP = &H10000
                RETURN_ERROR = &H20000
                TIME_OUT = &H10000
            End Enum
#End Region
#Region "Properties"
            ' parameter interface
            Property SpeedOverrideRatio As Single = 1                   ' inherited from old library
            ReadOnly Property PulsePerUnit As Double                       ' read from pData
                '-------------------
                '   Hsien , 2014.11.18
                '-------------------
                Get
                    Return __motorSetting.PulsePerUnit
                End Get
            End Property
            WriteOnly Property AllowedSpeedRange As Double '= AmaxVelocityMax      ' v change usage
                Set(value As Double)
                    AMaxM4_Fix_Speed_Range(MotorIndex, value)
                End Set
            End Property

            'calculation tool
            Public Function pulse2Unit(ByVal pulse As Double) As Double
                Return pulse / __motorSetting.PulsePerUnit
            End Function
            Public Function unit2Pulse(ByVal unit As Double) As Double
                Return unit * __motorSetting.PulsePerUnit 'PulsePerUnit
            End Function
            Public Function enum2cPoint(__enum As [Enum]) As cMotorPoint
                'Hsien , 2015.01.19
                ' would be truncated after turns data-pair from (enum,short) to (enum,cMotorPoint)
                Return pData.MotorPoints(PositionDictionary(__enum))
            End Function
            ReadOnly Property Setting As cMotorSetting
                Get
                    Return __motorSetting
                End Get
            End Property
            Protected __motorSetting As cMotorSetting   ' belong to this motor
#Region "Path Table Settings / Simultanous move"
            'used to store motor point ready to execute iterationally , Hsien , 2014.09.27
            Property PointTable As List(Of cMotorPoint) ' = New List(Of cMotorPoint)   'inject the MotorPoints as simultanous commands
                Get
                    Return __pointTable
                End Get
                Set(value As List(Of cMotorPoint))
                    __pointTable = value
                End Set
            End Property
            Protected __pointTable As List(Of cMotorPoint) = New List(Of cMotorPoint)
#End Region

            ' setup interface
            Property MotorIndex As Integer
                Get
                    Return __motorIndex
                End Get
                Set(value As Integer)
                    '--------------
                    '   BAD: coupled with pData
                    '--------------
                    If (pData.MotorSettings(value) IsNot Nothing) Then
                        __motorIndex = value
                        __motorSetting = pData.MotorSettings(value)
                        __dummyPoint.AxisIndex = __motorIndex
                        PositionToleranceInUnit = 0.5  'default 0.5 units , i.e 0.5mm , 0.5deg
                    End If

                End Set
            End Property
            Protected __motorIndex As Integer = -1  ' to avoid non-mapping error happed , Hsien , 2016.03.09
            Public Delegate Function commandFunctionPrototype(ByRef subState As Short) As statusEnum
            <Browsable(False)>
            Property CommandFunctionDictionary As Dictionary(Of [Enum], commandFunctionPrototype) = New Dictionary(Of [Enum], commandFunctionPrototype)    ' managed all command functions
            Property PositionDictionary As Dictionary(Of [Enum], Short) = New Dictionary(Of [Enum], Short)  ' mapping the local position index to global position index
            Property TimeoutLimit As TimeSpan
                Get
                    Return timeOutTimer.TimerGoal
                End Get
                Set(value As TimeSpan)
                    timeOutTimer.TimerGoal = value
                End Set
            End Property
            Property PositionToleranceInUnit As Double
                Get
                    Return pulse2Unit(PositionTolerance)
                End Get
                Set(value As Double)
                    PositionTolerance = unit2Pulse(value)
                End Set
            End Property
            Property PositionTolerance As Integer = 800      'unit in pulses, used in potion with check

            'alarm decoding
            ReadOnly Property ReturnError As returnErrorCodes
                Get
                    Return [Enum].ToObject(GetType(returnErrorCodes), Me.__returnStatus)
                End Get
            End Property
            ReadOnly Property MotionStatus As motionStatusEnum
                Get
                    Return [Enum].ToObject(GetType(motionStatusEnum), Me.__motionStatus)
                End Get
            End Property
            ReadOnly Property ErrorStatus As errorStatusEnum
                Get
                    Return [Enum].ToObject(GetType(errorStatusEnum), Me.__errorStatus)
                End Get
            End Property

            ' monitor interface
            ReadOnly Property ElapsedTime As TimeSpan
                Get
                    'Return timeOutCount
                    Return timeOutTimer.TimeElapsed
                End Get
            End Property
            ReadOnly Property ExecuteStatus As Object
                Get
                    Return commandInExecute
                End Get
            End Property
            ReadOnly Property CommandPosition As Integer
                Get
                    AMaxM4_Get_Command(Me.MotorIndex, Me.commandCounter)
                    '__returnStatus = AMaxM4_Get_Command(Me.MotorIndex, Me.commandCounter)
                    Return commandCounter
                End Get
            End Property
            ReadOnly Property FeedBackPosition As Integer
                Get
                    AMaxM4_Get_Position(Me.MotorIndex, Me.feedBackCounter)
                    '__returnStatus = AMaxM4_Get_Position(Me.MotorIndex, Me.feedBackCounter)
                    Return feedBackCounter
                End Get
            End Property
            ReadOnly Property ErrorPosition As Integer
                Get
                    AMaxM4_Get_Error_Counter(Me.MotorIndex, Me.errorCounter)
                    '__returnStatus = AMaxM4_Get_Error_Counter(Me.MotorIndex, Me.errorCounter)
                    Return errorCounter
                End Get
            End Property
            Property AcceptableErrorStatus As errorStatusEnum
                Get
                    Return __accepatableErrorStatus
                End Get
                Set(ByVal value As errorStatusEnum)
                    __accepatableErrorStatus = value
                End Set
            End Property
            ReadOnly Property CurrentSpeed As Double
                Get
                    AMaxM4_Get_Current_Speed(Me.MotorIndex, __currentSpeed)
                    Return __currentSpeed
                End Get
            End Property

            ReadOnly Property CommandSubState As motionSubStateEnum
                Get
                    Return __commandSubState
                End Get
            End Property
            '------------------------------------
            '   Individual axis Slow-Down switch setting
            '   Hsien , 2015.01.19
            '------------------------------------
            Property SlowdownEnable As enableEnum
                Get
                    Return __sdEnabled
                End Get
                Set(value As enableEnum)
                    __sdEnabled = value
                    __returnStatus = AMaxM4_Set_SD_Enable(Me.MotorIndex,
                                             __sdEnabled,
                                             __motorSetting.SlowDownLevel,
                                             __sdLatchSetting,
                                             __sdModeSetting)

                End Set
            End Property
            Property SlowdownLatch As sdLatchEnum
                Get
                    Return __sdLatchSetting
                End Get
                Set(value As sdLatchEnum)
                    __sdLatchSetting = value
                    __returnStatus = AMaxM4_Set_SD_Enable(Me.MotorIndex,
                                            __sdEnabled,
                                            __motorSetting.SlowDownLevel,
                                            __sdLatchSetting,
                                            __sdModeSetting)
                End Set
            End Property
            Property SlowdownMode As sdModeEnum
                Get
                    Return __sdModeSetting
                End Get
                Set(value As sdModeEnum)
                    __sdModeSetting = value
                    __returnStatus = AMaxM4_Set_SD_Enable(Me.MotorIndex,
                                            __sdEnabled,
                                            __motorSetting.SlowDownLevel,
                                            __sdLatchSetting,
                                            __sdModeSetting)
                End Set
            End Property
            '------------------------------------
            '   Synchron Master Mode switch setting
            '   Hsien , 2015.03.24
            '------------------------------------
            Property SynchronMode As syncMode = syncMode.IMMEDIATELY_START
            Property SynchonSignalMode As syncSignalMode = syncSignalMode.WHEN_START_ACCELERATION
            '------------------------------------
            '   Start All/Stop All hardware setting
            '   Hsien , 2015.01.19
            '------------------------------------
            Property StartAllMode As moveAllMode = moveAllMode.LEVEL
            Property StartAllRisingOrFalling As moveAllFallOrRise = moveAllFallOrRise.RISING_OR_HIGH
            Property StopAllMode As moveAllMode = moveAllMode.LEVEL
            Property StopAllRisingOrFalling As moveAllFallOrRise = moveAllFallOrRise.FALLING_OR_LOW
#End Region

            Property PositionPoint As cMotorPoint 'Hsien , 2015.12.21, used when single position moving going to execute
                Get
                    If PointTable.Count = 0 Then
                        Return __dummyPoint
                    End If

                    Return __pointTable.First
                End Get
                Set(value As cMotorPoint)
                    __pointTable.Clear()
                    __pointTable.Add(value)
                End Set
            End Property
            ' First Byte(MSB) - AMAXERROR or DEVICEERROR
            ' Seconde Byte - categrory
            ' Last WORD - reason
            Protected __motionStatus As UShort = 0
            Protected __returnStatus As Short = 0
            Protected __errorStatus As UInteger = 0
            Protected __accepatableErrorStatus As UInteger = 0           'Hsien , 2015.01.19 , the stop reason filter

            Protected timeOutTimer As singleTimer = New singleTimer With {.TimerGoal = New TimeSpan(0, 0, 30)}
            Protected timeHomeStable As singleTimer = New singleTimer With {.TimerGoal = New TimeSpan(0, 0, 1)}
            Protected __sdEnabled As enableEnum = enableEnum.DISABLE
            Protected __sdLatchSetting As sdLatchEnum = sdLatchEnum.DO_NOT_LATCH
            Protected __sdModeSetting As sdModeEnum = sdModeEnum.SLOW_DOWN_ONLY

            Protected commandCounter As Integer = 0
            Protected feedBackCounter As Integer = 0
            Protected errorCounter As Integer = 0
            Protected __currentSpeed As Double = 0
            Protected __dummyPoint As cMotorPoint = New cMotorPoint

            ' control interface
            ReadOnly Property CommandEndStatus As statusEnum
                '--------------------
                '   Use to implement auto-reset mechanism
                ' Hsien , 2015.01.10
                '--------------------
                Get
                    If (commandInExecute.Equals(motorCommandEnum.WAIT_RECALL)) Then
                        resetCommand()
                    End If
                    Return __commandEndStatus
                End Get
            End Property
            Protected __commandEndStatus As statusEnum = statusEnum.EXECUTING
            'internal

            Protected __commandSubState As Short = 0                          ' shared by all command functions , command function should rewind this in the end of execution
            Protected commandInExecute As [Enum]
            Protected commandBeforePause As [Enum]          ' used to back the command in execute before motion paused , Hsien , 2014.09.14

            Protected positionIndex As Integer = 0        ' position command use

            Public alarmPackage As alarmContextBase
            'initializing
            Public Sub New()
                CommandFunctionDictionary.Add(motorCommandEnum.GO_HOME, AddressOf homeCommand)
                CommandFunctionDictionary.Add(motorCommandEnum.GO_POSITION, AddressOf synchronMasterMoveCommand)

                CommandFunctionDictionary.Add(motorCommandEnum.GO_POSITION_OPEN_LOOP, AddressOf synchronMasterMoveCommand)
                CommandFunctionDictionary.Add(motorCommandEnum.JOG, AddressOf jogCommand)

                CommandFunctionDictionary.Add(motorCommandEnum.STOP_FORCELY, Function() (statusEnum.EXECUTION_END))
                CommandFunctionDictionary.Add(motorCommandEnum.V_CHANGE, AddressOf vchangeCommand)    'Hsien , 2014.09.26

                CommandFunctionDictionary.Add(motorCommandEnum.GO_POSITION_COMBINED, AddressOf combinedOpenPositionCommand)    ' Hsien , 2014.09.26

                '-------------------------------
                '   Simultanous control
                '-------------------------------
                'CommandFunctionDictionary.Add(motorCommandEnum.START_MOVE, AddressOf simultanousMasterMoveCommand)
                'CommandFunctionDictionary.Add(motorCommandEnum.START_MOVE_SLAVE, AddressOf simultanousSlaveMove)    'Hsien , 2015.01.23
                '--------------------------------
                '   Stop/Pause/Resume motion
                '--------------------------------
                CommandFunctionDictionary.Add(motorCommandEnum.WAIT_MOTION_STOP, AddressOf amaxWaitMotionStop)          'Hsien , 2015.01.23
                CommandFunctionDictionary.Add(motorCommandEnum.MOTION_PAUSE, Function() (statusEnum.EXECUTION_END))    ' Hsien , 2014.09.26
                CommandFunctionDictionary.Add(motorCommandEnum.MOTION_RESUME, Function() (statusEnum.EXECUTION_END))    ' Hsien , 2014.09.26
                CommandFunctionDictionary.Add(motorCommandEnum.STOP_SLOW_DOWN, AddressOf slowDownCommand)    ' Hsien ,2014.10.09

                CommandFunctionDictionary.Add(motorCommandEnum.SYNCHRON_MASTER, AddressOf synchronMasterMoveCommand)    ' Hsien ,2015.03.24


                commandInExecute = motorCommandEnum.NONE

                ' alarm package initailizing
                alarmPackage = New alarmContentMotor()
                With alarmPackage
                    .Sender = Me
                    '.PossibleResponse = alarmContextBase.responseWays.RETRY
                    .PossibleResponse = alarmContextBase.responseWays.ABORT
                    '.CallbackResponse(alarmContextBase.responseWays.ABORT) = alarmContextBase.abortMethod   'too early , Hsien ,2015.10.12
                End With

            End Sub

            '-------------------
            ' control interface
            '-------------------
            Public Function drive(ByVal command As motorCommandEnum, ByVal globalCommandPosition As cMotorPoint) As statusEnum
                'the extension alias function , hsien , 2015.01.05
                If (commandInExecute.Equals(motorCommandEnum.NONE)) Then
                    'avoid to corrupt the thread-shared data structure - pointTable , Hsien , 2016.02.15
                    PositionPoint = globalCommandPosition
                End If
                Return drive(command)
            End Function

            Public Function drive(ByVal command As [Enum], Optional ByVal commandPosition As [Enum] = Nothing) As statusEnum



                '-------------------------------------------------------------
                '   when command UNDETERMINED , will do initializing procedure
                '-------------------------------------------------------------
                ' first priority command : STOP_FORCELY
                If (command.Equals(motorCommandEnum.STOP_FORCELY)) Then
                    '-----------------------------
                    ' highest priority command
                    'direct firing command
                    '------------------------------
                    timeOutTimer.IsEnabled = False
                    __commandSubState = motionSubStateEnum.SETUP_AND_GO
                    commandInExecute = motorCommandEnum.NONE
                    __commandEndStatus = statusEnum.EXECUTION_END

                    'Hsien , 2014.10.09 , Slow down stop need time to down , have to use drive-command sequence control
                    '__returnStatus = AMaxM4_SlowDownStop(MotorIndex)
                    __returnStatus = AMaxM4_Soft_Emg_Stop(MotorIndex)

                    '----------------
                    '   Wait until motor stopped
                    '----------------
                    While (__motionStatus <> 0)
                        __returnStatus = AMaxM4_MotionDone(MotorIndex, __motionStatus)
                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If
                    End While

                    __returnStatus = AMaxM4_ErrorStatus(MotorIndex, __errorStatus)

                    Return statusEnum.EXECUTION_END

                End If

                '------------------
                '   PAUSE AND RESUME
                '------------------
                If (command.Equals(motorCommandEnum.MOTION_PAUSE) _
                    And Not commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE)) Then
                    pauseCommand()
                    commandBeforePause = commandInExecute
                    commandInExecute = motorCommandEnum.MOTION_PAUSE
                    Return statusEnum.EXECUTING
                End If

                If (commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE) _
                   And Not command.Equals(motorCommandEnum.MOTION_RESUME)) Then
                    ' only resume command able to release pause command
                    Return statusEnum.EXECUTING 'reject
                End If

                If (Not commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE) _
                  And command.Equals(motorCommandEnum.MOTION_RESUME)) Then
                    ' only resume command able to release pause command
                    Return statusEnum.EXECUTING 'reject , Hsien , 2014.10.20 , found that in section lock , the pause-resume pair may be decoupled
                End If

                If (commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE) _
                    And command.Equals(motorCommandEnum.MOTION_RESUME)) Then
                    resumeCommand()
                    commandInExecute = commandBeforePause
                    'Return statusEnum.EXECUTION_END
                    Return statusEnum.EXECUTING
                End If
                '------------------------
                '   STOP SLOWLY , Hsien  2015.01.10
                '-------------------------
                If (command.Equals(motorCommandEnum.STOP_SLOW_DOWN) And
                    (Not commandInExecute.Equals(motorCommandEnum.STOP_SLOW_DOWN)) And
                    (Not commandInExecute.Equals(motorCommandEnum.WAIT_RECALL))) Then
                    '-----------------------------
                    ' high priority command ,would override previous in-execute command
                    'Hsien , 2015.01.10
                    '------------------------------
                    resetCommand()
                    commandInExecute = motorCommandEnum.STOP_SLOW_DOWN
                    timeOutTimer.IsEnabled = True
                    Return statusEnum.EXECUTING
                End If


                ' when command is executed to the end , do not accecpt next command 
                ' caller should determine if recall by recognizing return states in this device
                If (commandInExecute.Equals(motorCommandEnum.WAIT_RECALL)) Then
                    '----------------------------------
                    ' last command had been executed
                    '   1. rewind execution status
                    '   2. return the execution result 
                    '----------------------------------
                    commandInExecute = motorCommandEnum.NONE
                    Return __commandEndStatus
                End If

                If (Not commandInExecute.Equals(motorCommandEnum.NONE)) Then
                    ' command on execute , reject command override
                Else
                    If (commandPosition IsNot Nothing) Then
                        '----------------------------------------------------------------
                        'Hsien , 2014.11.14
                        'point indicated , fetch point data from pData tank
                        '----------------------------------------------------------------
                        If (PositionDictionary.TryGetValue(commandPosition, positionIndex)) Then
                            PositionPoint = pData.MotorPoints(positionIndex)
                        Else
                            '---------------------------------
                            '   Key Not Found , Reject Command
                            '---------------------------------
                            ' raising alarm : exception
                            __returnStatus = returnErrorCodes.ERR_No_Assigning_Point
                            CentralAlarmObject.raisingAlarm(alarmPackage)
                            Return statusEnum.EXECUTION_END_FAIL
                        End If
                    Else
                        '----------------------------------------------------------------
                        'Hsien , 2014.11.14
                        '   No corresponding point indicated , use  positionPoint instead
                        '   if positionPoint not assigned , motor would generate error when executing
                        '----------------------------------------------------------------
                    End If

                    timeOutTimer.IsEnabled = True   'start time out checking for all command , Hsien , 2014.10.09
                    commandInExecute = command

                End If

                Return statusEnum.EXECUTING

            End Function

            ' internal
            Protected Overrides Function process() As Integer

                '---------------------------------
                'if alarmed , pause all motions? , answer : no , controlled by Host
                '---------------------------------

                '--------------------------
                '   drive the state-machine
                '--------------------------
                If (commandInExecute.Equals(motorCommandEnum.NONE) Or
                    commandInExecute.Equals(motorCommandEnum.WAIT_RECALL) Or
                    commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE)) Then
                    ' performance considering
                    Return 0
                End If

                __commandEndStatus = CommandFunctionDictionary(commandInExecute)(__commandSubState)
                If (__commandEndStatus And statusEnum.EXECUTION_END) Then
                    ' bitwise operation
                    ' reset command
                    timeOutTimer.IsEnabled = False
                    __commandSubState = motionSubStateEnum.SETUP_AND_GO
                    commandInExecute = motorCommandEnum.WAIT_RECALL
                End If

                If (__commandEndStatus = statusEnum.EXECUTION_END_FAIL) Then
                    '----------------------------
                    'error occured
                    'raise alarm and rewind state
                    '----------------------------
                    alarmPackage.CallbackResponse(alarmContextBase.responseWays.ABORT) = alarmContextBase.abortMethod   'Hsien , setup before raise alarm  , 2015.10.12
                    CentralAlarmObject.raisingAlarm(alarmPackage)       'raising alarm
                    'stop motor and reset all status


                End If

                Return 0
            End Function

#Region "command functions"

            '--------------------------------
            '   Solo commands
            '--------------------------------
            Protected Function homeCommand(ByRef subState As Short) As statusEnum
                ' home process sub state-mahcine
                ' return as state
                Select Case subState
                    Case motionSubStateEnum.SETUP_AND_GO

                        Dim homePoint As cMotorPoint

                        ' clear state of MAmax_HoHome state-machine

                        'searching the corresponding point
                        homePoint = pData.MotorPoints.Find(Function(__point As cMotorPoint) (__point.IsHomePoint And __point.AxisIndex = __motorIndex))

                        __pointTable.Clear()
                        __pointTable.Add(homePoint)   'used to fetch waitStop command

                        If (homePoint Is Nothing) Then
                            __returnStatus = returnErrorCodes.ERR_No_Corresponding_Point
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        ' set-up velocity profile
                        With homePoint

                            If (homePoint.VelocityProfile = velocityProfileEnum.S_CURVE) Then
                                __returnStatus = AMaxM4_SMovSet(__motorIndex,
                                                                .StartVelocity,
                                                                .Velocity,
                                                                .AccelerationTime,
                                                                .DecelerationTime,
                                                                0,
                                                                0)
                            Else
                                __returnStatus = AMaxM4_TMovSet(__motorIndex,
                                                                .StartVelocity,
                                                                .Velocity,
                                                                .AccelerationTime,
                                                                .DecelerationTime)
                            End If

                            If (__returnStatus <> 0) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If


                            ' set-up home mode
                            __returnStatus = AMaxM4_HomeConfig(__motorIndex,
                                                               1,
                                                               __motorSetting.HomeLevel,
                                                               0,
                                                               0,
                                                               0)
                            If (__returnStatus <> 0) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If


                            ' start home moving
                            __returnStatus = AMaxM4_HomeMove(__motorIndex,
                                                             .PointType,
                                                             .Distance)
                            If (__returnStatus <> 0) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If
                        End With


                        '----------------------------
                        '   Use asynchronous thread to examine
                        '----------------------------
                        examinationList.Clear()
                        examinationList.AddRange({AddressOf amaxWaitMotionStop,
                                             AddressOf amaxWaitFinePosition})

                        handle = New motionStateObject
                        ThreadPool.QueueUserWorkItem(AddressOf waitMotionStopCommand, handle)
                        'handle = waitMotionComplete.BeginInvoke(Nothing, Nothing)

                        subState = motionSubStateEnum.WAITEXECUTION

                    Case motionSubStateEnum.WAITEXECUTION
                        If (handle.IsCompleted) Then
                            If (handle.result = statusEnum.EXECUTION_END) Then
                                timeHomeStable.IsEnabled = True
                                subState = 100
                            Else
                                Return statusEnum.EXECUTION_END_FAIL    '
                            End If
                        End If
                       
                    Case 100
                        If timeHomeStable.IsTimerTicked Then
                            '---------------------
                            '   motor in position , reset counters
                            '---------------------
                            AMaxM4_CmdPos_Reset(MotorIndex)
                            subState = motionSubStateEnum.SETUP_AND_GO
                            Return statusEnum.EXECUTION_END
                        End If

                End Select

                Return statusEnum.EXECUTING

            End Function

            Protected Function jogCommand(ByRef subState As Short) As statusEnum

                'Hsien , 2014.07.14
                ' follow the teaching speed
                If (PositionPoint Is Nothing) Then
                    __returnStatus = returnErrorCodes.ERR_No_Corresponding_Point
                    Return statusEnum.EXECUTION_END_FAIL
                End If

                If (PositionPoint.VelocityProfile = velocityProfileEnum.S_CURVE) Then
                    __returnStatus = AMaxM4_SMovSet2(Me.MotorIndex, PositionPoint)
                Else
                    __returnStatus = AMaxM4_TMovSet(Me.MotorIndex, PositionPoint.StartVelocity, PositionPoint.Velocity, PositionPoint.AccelerationTime, PositionPoint.DecelerationTime)
                End If

                If (__returnStatus <> 0) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If

                Dim dir As Short = 0
                If (PositionPoint.Distance > 0) Then
                    dir = 1
                Else
                    dir = 0
                End If

                '-----------
                '   1: forward
                '   2: backward
                ''----------
                __returnStatus = AMaxM4_VMov(Me.MotorIndex, dir) ' according to point setting

                If (__returnStatus <> 0) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If


                '------------------------------------------------
                '   Prevent JOG error , Hsien , 2015.10.16
                '------------------------------------------------
                __returnStatus = AMaxM4_MotionDone(MotorIndex, __motionStatus)
                __returnStatus = AMaxM4_ErrorStatus(MotorIndex, __errorStatus)

                If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If

                If (__motionStatus = motionStatusEnum._STOP AndAlso
                    (__errorStatus And (Not __accepatableErrorStatus))) Then
                    '-----------------------------------
                    '   motion stopped , and not acceptable
                    '   Error Occured
                    '-----------------------------------
                    Return statusEnum.EXECUTION_END_FAIL    'abnormal stopped
                End If


                Return statusEnum.EXECUTION_END
            End Function
            Protected Function openPositionCommand(ByRef subState As Short) As statusEnum

                '---------
                '   OPEN LOOP Control (Send Pulse Without feed back check)
                ' Hsien , 2014.09.17
                '---------

                Select Case subState
                    Case motionSubStateEnum.SETUP_AND_GO

                        'PositionPoint = pData.MotorPoints(positionIndex)

                        'Hsien , 2014.11.14
                        ' follow the assigned point
                        If (PositionPoint Is Nothing) Then
                            __returnStatus = returnErrorCodes.ERR_No_Corresponding_Point
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        'Hsien , 2014.07.11
                        With PositionPoint

                            If (.VelocityProfile = velocityProfileEnum.S_CURVE) Then
                                __returnStatus = AMaxM4_SMovSet(.AxisIndex _
                                                   , .StartVelocity _
                                                       , .Velocity _
                                                       , .AccelerationTime _
                                                       , .DecelerationTime _
                                                       , 0 _
                                                       , 0)
                            Else
                                __returnStatus = AMaxM4_TMovSet(.AxisIndex _
                                                                                  , .StartVelocity _
                                                                                      , .Velocity _
                                                                                      , .AccelerationTime _
                                                                                      , .DecelerationTime)
                            End If


                            If .PointType = pointTypeEnum.ABS Then  ' Abs-Move
                                __returnStatus = AMaxM4_AMov(MotorIndex, CInt(PositionPoint.Distance))
                            Else ' Rel-Move
                                __returnStatus = AMaxM4_RMov(MotorIndex, CInt(PositionPoint.Distance))
                            End If

                            If (__returnStatus <> 0) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If

                        End With

                        __pointTable.Clear()
                        __pointTable.Add(PositionPoint)   'used to fetch waitMotionStop

                        examinationList.Clear()
                        examinationList.Add(AddressOf amaxWaitMotionStop)

                        handle = New motionStateObject
                        ThreadPool.QueueUserWorkItem(AddressOf waitMotionStopCommand, handle)
                        'handle = waitMotionComplete.BeginInvoke(Nothing, Nothing)

                        subState = motionSubStateEnum.WAITEXECUTION
                    Case motionSubStateEnum.WAITEXECUTION                   
                        If (handle.IsCompleted) Then
                            Return handle.result
                        End If
                End Select

                Return statusEnum.EXECUTING
            End Function
            Protected Function semiCloseLoopPositionCommand(ByRef subState As Short) As statusEnum
                'todo , keep positioning until error less than window
                ' Hsien , on build , 2014.09.18
                Return statusEnum.EXECUTING
            End Function
            Protected Function vchangeCommand(ByRef subState As Short) As statusEnum
                '--------------
                '   Hsien , 2014.09.26
                '--------------
                ' not works , need some time to let this setting avaialble
                '__returnStatus = AMaxM4_Fix_Speed_Range(Me.MotorIndex, Me.AllowedSpeedRange)

                'Hsien , 2014.11.14
                ' follow the assigned point
                If (PositionPoint Is Nothing) Then
                    __returnStatus = returnErrorCodes.ERR_No_Corresponding_Point
                    Return statusEnum.EXECUTION_END_FAIL
                End If


                __returnStatus = AMaxM4_V_Change(Me.MotorIndex _
                                                 , PositionPoint.Velocity _
                                                 , PositionPoint.AccelerationTime)
                If (__returnStatus <> 0) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If

                Return statusEnum.EXECUTION_END
            End Function
            Protected Function combinedOpenPositionCommand(ByRef subState As Short) As statusEnum

                '---------------------------
                '   Run combined velocity-position profile at once , according to pointsStored in pathtable
                '---------------------------

                Select Case subState
                    Case motionSubStateEnum.SETUP_AND_GO
                        ' reset register of slave
                        __returnStatus = AMaxM4_Reset_Path(Me.MotorIndex)

                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        ' setup speed ( unknown purpose)
                        __returnStatus = AMaxM4_Set_Path_Move_Speed(Me.MotorIndex _
                                                                    , velocityProfileType.S_CURVE _
                                                                    , AmaxVelocityMin _
                                                                    , AmaxVelocityMax _
                                                                    , __pointTable(0).AccelerationTime _
                                                                    , __pointTable(0).DecelerationTime)
                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        ' inject (queue-in) points into register
                        For Each __point As cMotorPoint In PointTable

                            If (__point.PointType = pointTypeEnum.ABS) Then 'abs move
                                __returnStatus = AMaxM4_Set_Path_Line_Data(Me.MotorIndex _
                                                                           , commandFunctionLineEnum.START_A_MOVE _
                                                                           , __point.Distance _
                                                                           , __point.StartVelocity _
                                                                           , __point.Velocity _
                                                                           , enableDeceleration.ENABLE)
                            Else
                                __returnStatus = AMaxM4_Set_Path_Line_Data(Me.MotorIndex _
                                                                           , commandFunctionLineEnum.START_R_MOVE _
                                                                           , __point.Distance _
                                                                           , __point.StartVelocity _
                                                                           , __point.Velocity _
                                                                           , enableDeceleration.ENABLE)
                            End If

                            If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If

                        Next

                        'execute
                        __returnStatus = AMaxM4_Start_Path(Me.MotorIndex)

                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If


                        examinationList.Clear()
                        examinationList.AddRange({AddressOf amaxWaitMotionStop,
                                                  AddressOf amaxWaitFinePosition})

                        handle = New motionStateObject
                        ThreadPool.QueueUserWorkItem(AddressOf waitMotionStopCommand, handle)


                        subState = motionSubStateEnum.WAITEXECUTION

                    Case motionSubStateEnum.WAITEXECUTION
                        If (handle.IsCompleted) Then
                            Return handle.result
                        End If


                End Select


                Return statusEnum.EXECUTING

            End Function
            ''-------------------------------
            ''   Simultanous control
            ''-------------------------------
            'Protected Function simultanousMasterMoveCommand(ByRef subState As Short) As statusEnum
            '    'Hsien , 2014.10.3
            '    ' move with slave motors
            '    Select Case subState
            '        Case motionSubStateEnum.SETUP_AND_GO
            '            '---------------------
            '            'Support Model - 124X Only
            '            'Hsien , 2015.01.26
            '            '---------------------
            '            __returnStatus = AMaxM4_Set_Move_All_Stop_Mode(Me.MotorIndex, StopAllMode, StopAllRisingOrFalling)

            '            If (__returnStatus <> 0) Then
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '            'SimultanouseSetting = SimultanouseSetting
            '            __returnStatus = tableSetup()
            '            If (__returnStatus <> 0) Then
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If
            '            '--------------------------------------------
            '            '   Command content must be setted before
            '            '--------------------------------------------
            '            __returnStatus = AMaxM4_Start_Move_All(Me.MotorIndex)

            '            If (__returnStatus <> 0) Then
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '            timeOutTimer.IsEnabled = True
            '            subState = motionSubStateEnum.WAITEXECUTION

            '        Case motionSubStateEnum.WAITEXECUTION
            '            'Hsien , 2015.03.23 ' should wait all simultanous move stop

            '            Dim isAllMotionDone As Boolean = PointTable.TrueForAll(Function(obj As cMotorPoint) As Boolean
            '                                                                       Dim __________motionStatus As UShort
            '                                                                       'accumulate all return status
            '                                                                       __returnStatus = __returnStatus Or AMaxM4_MotionDone(obj.AxisIndex, __________motionStatus)
            '                                                                       Return (__________motionStatus = motionStatusEnum._STOP) ' check if motion stopped
            '                                                                   End Function)
            '            '__returnStatus = AMaxM4_MotionDone(MotorIndex, __motionStatus)

            '            If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '            'If (__motionStatus = 0) Then
            '            If (isAllMotionDone) Then
            '                '-----------------------------------
            '                '   motion(all) stopped , go futher check (for all motor status
            '                '-----------------------------------
            '                For Each __point As cMotorPoint In PointTable

            '                    __returnStatus = AMaxM4_ErrorStatus(__point.AxisIndex, __errorStatus)

            '                    If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
            '                        Return statusEnum.EXECUTION_END_FAIL
            '                    End If

            '                    If (__errorStatus = 0 Or
            '                        __errorStatus = __accepatableErrorStatus) Then
            '                        '--------------------
            '                        '   Done Successfully , rewind
            '                        '--------------------
            '                        subState = motionSubStateEnum.SETUP_AND_GO
            '                        Return statusEnum.EXECUTION_END
            '                    Else
            '                        '---------------
            '                        '   Abnormally stopped
            '                        '---------------
            '                        Return statusEnum.EXECUTION_END_FAIL
            '                    End If

            '                Next

            '            ElseIf (timeOutTimer.IsTimerTicked) Then
            '                '----------------
            '                '   Error Occured
            '                '----------------
            '                __returnStatus = returnErrorCodes.ERR_Execution_Time_Out
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '    End Select


            '    Return statusEnum.EXECUTING
            'End Function
            'Protected Function simultanousSlaveMove(ByRef subState As Short) As statusEnum
            '    'need check , Hsien , 2015.01.23
            '    'command fired by other motors , polling self-status until stopped

            '    Select Case subState
            '        Case motionSubStateEnum.SETUP_AND_GO
            '            '------------------------------
            '            '   Wait trigger
            '            '------------------------------
            '            __returnStatus = AMaxM4_MotionDone(Me.MotorIndex, __motionStatus)
            '            If (__returnStatus <> 0) Then
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '            If (__motionStatus = motionStatusEnum.ACCELERATING) Then
            '                subState = motionSubStateEnum.WAITEXECUTION
            '            End If

            '        Case motionSubStateEnum.WAITEXECUTION

            '            __returnStatus = AMaxM4_MotionDone(MotorIndex, __motionStatus)
            '            If (__motionStatus = 0) Then
            '                '--------------------
            '                '   Done , check reason
            '                '--------------------
            '                __returnStatus = AMaxM4_ErrorStatus(MotorIndex, __errorStatus)
            '                If (__errorStatus = 0 Or
            '                    (__errorStatus And __accepatableErrorStatus)) Then
            '                    '----------------------------------
            '                    '   Hsien , 2015.01.19
            '                    '   normal stopped , or stopped by acceptable reason are in expect
            '                    '----------------------------------
            '                    Return statusEnum.EXECUTION_END
            '                Else
            '                    Return statusEnum.EXECUTION_END_FAIL
            '                End If
            '            ElseIf (timeOutTimer.IsTimerTicked) Then
            '                '---------------------
            '                '   Wait until timeout
            '                '---------------------
            '                Return statusEnum.EXECUTION_END_FAIL
            '            End If

            '    End Select

            '    Return statusEnum.EXECUTING
            'End Function
            '--------------------------------
            '   Stop/Pause/Resume motion
            '--------------------------------
            Protected Function slowDownCommand(ByRef subState As Short) As statusEnum

                Select Case subState
                    Case motionSubStateEnum.SETUP_AND_GO
                        __returnStatus = AMaxM4_SlowDownStop(Me.MotorIndex)
                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        examinationList.Clear()
                        examinationList.Add(AddressOf amaxWaitMotionStop)

                        'Hsien , raised only when wait stop method not invoked yet , 2016.02.15
                        If (handle Is Nothing OrElse
                            handle.IsCompleted) Then

                            handle = New motionStateObject
                            ThreadPool.QueueUserWorkItem(AddressOf waitMotionStopCommand, handle)

                        End If


                        subState = motionSubStateEnum.WAITEXECUTION

                    Case motionSubStateEnum.WAITEXECUTION
                        If (handle.IsCompleted) Then
                            Return handle.result
                        End If

                End Select

                Return statusEnum.EXECUTING
            End Function

            Protected handle As motionStateObject = Nothing  'used to monitor 

            Class motionStateObject
                Implements IAsyncResult

                Friend __isCompleted As Boolean = False
                Friend result As statusEnum = statusEnum.EXECUTING

                Public ReadOnly Property AsyncState As Object Implements IAsyncResult.AsyncState
                    Get
                        Return Nothing
                    End Get
                End Property
                Public ReadOnly Property AsyncWaitHandle As WaitHandle Implements IAsyncResult.AsyncWaitHandle
                    Get
                        Return Nothing

                    End Get
                End Property
                Public ReadOnly Property CompletedSynchronously As Boolean Implements IAsyncResult.CompletedSynchronously
                    Get
                        Return Nothing

                    End Get
                End Property
                Public ReadOnly Property IsCompleted As Boolean Implements IAsyncResult.IsCompleted
                    Get
                        Return Me.__isCompleted
                    End Get
                End Property
            End Class

            Protected examinationList As List(Of Func(Of statusEnum)) = New List(Of Func(Of statusEnum))

            '-----------------------------------
            '   Thread Schedulaing
            '-----------------------------------
            Shared incomingSleepTime = 100
            Shared incrementSleepTime = 1
            Shared minSleepTime = 10
            Shared entryCount As Integer = 0    'used to count how many thread had entried
            Sub waitMotionStopCommand(state As motionStateObject) 'As statusEnum

                Interlocked.Increment(entryCount) 'atomic operation
                Thread.Sleep(incomingSleepTime)   'delay a while then start polling (wait command had accepted by module)


                'break condition:
                '1. first function returned EXECUTION_FAILED
                '2. or last function returned EXECUTION_END
                With state
                    For index = 0 To examinationList.Count - 1

                        timeOutTimer.IsEnabled = True   'restart timer when examnition changes

                        'loop until target invoke end
                        While .result = statusEnum.EXECUTING

                            'Hsien ,activate timer when not in pause state
                            If (Not commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE) And
                                Not timeOutTimer.IsEnabled) Then
                                timeOutTimer.IsEnabled = True   'restart timer ,when changed examinition method
                            ElseIf (commandInExecute.Equals(motorCommandEnum.MOTION_PAUSE)) Then
                                timeOutTimer.IsEnabled = False  'suspend timer
                            End If

                            .result = examinationList(index).Invoke

                            If (timeOutTimer.IsTimerTicked And
                                .result = statusEnum.EXECUTING) Then
                                __returnStatus = returnErrorCodes.ERR_Execution_Time_Out
                                .result = statusEnum.EXECUTION_END_FAIL
                            End If

                            Thread.Sleep(minSleepTime + incrementSleepTime * entryCount) 'dynamic schedualing
                        End While

                        If (.result = statusEnum.EXECUTION_END_FAIL) Then
                            Exit For
                            'break for-loop, no need to do further check even though there'r other examnation
                        End If

                    Next

                    Interlocked.Decrement(entryCount) 'atomic operation
                    .__isCompleted = True   'mark-up 
                End With


            End Sub

            Protected Function amaxWaitMotionStop() As statusEnum
                '--------------------------------------------------------------------
                '   The  Motion Stop Check Routine both for Single/Multi PTP moving of amax
                '--------------------------------------------------------------------


                'Hsien , 2015.03.23 ' should wait all simultanous move stop


                Dim isAllMotionDone As Boolean = __pointTable.TrueForAll(Function(obj As cMotorPoint) As Boolean
                                                                             Dim eachMotionStatus As UShort
                                                                             'accumulate all return status
                                                                             __returnStatus = __returnStatus Or AMaxM4_MotionDone(obj.AxisIndex, eachMotionStatus)
                                                                             Return (eachMotionStatus = motionStatusEnum._STOP) ' check if motion stopped
                                                                         End Function)


                If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If



                If (isAllMotionDone) Then
                    '-----------------------------------
                    '   motion(all) stopped , go futher check (for all motor status
                    '-----------------------------------
                    For Each __point As cMotorPoint In __pointTable

                        __returnStatus = AMaxM4_ErrorStatus(__point.AxisIndex, __errorStatus)


                        If (__returnStatus <> returnErrorCodes.ERR_NoError) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                        If (__errorStatus And (Not __accepatableErrorStatus)) <> 0 Then
                            '---------------
                            '   Abnormally stopped
                            '---------------
                            'corrected alarm message , Hsien , 2015.11.02
                            alarmPackage = New alarmContentMotor With {.Sender = New motorControl With {.MotorIndex = __point.AxisIndex,
                                                                                                        .__errorStatus = __errorStatus,
                                                                                                        .__motionStatus = 0}}
                            Return statusEnum.EXECUTION_END_FAIL
                        End If

                    Next

                    '--------------------
                    '   Done Successfully , rewind
                    '--------------------
                    'all axis had no erros, Hsien , 2015.11.2
                    Return statusEnum.EXECUTION_END
                End If

                Return statusEnum.EXECUTING
            End Function
            Protected Function amaxWaitFinePosition() As statusEnum
                '--------------------------------------
                '   Hsien , 2015.11.02 , all position error have to converge below tolerance
                '--------------------------------------
                Dim isAllAxisApproched As Boolean = PointTable.TrueForAll(Function(__point As cMotorPoint) As Boolean

                                                                              __returnStatus = AMaxM4_Get_Command(__point.AxisIndex, commandCounter)
                                                                              __returnStatus = AMaxM4_Get_Position(__point.AxisIndex, feedBackCounter)

                                                                              Return Math.Abs(commandCounter - feedBackCounter) < PositionTolerance

                                                                          End Function)


                If (__returnStatus <> 0) Then
                    Return statusEnum.EXECUTION_END_FAIL
                End If

                If (isAllAxisApproched) Then
                    '---------------------
                    '   System in position
                    '---------------------
                    Return statusEnum.EXECUTION_END
                End If

                Return statusEnum.EXECUTING
            End Function
            '-------------------------------
            '   Sync motion
            '-------------------------------
            Protected Function synchronMasterMoveCommand(ByRef subState As Short) As statusEnum
                'Hsien , 2014.10.3
                ' move with slave motors
                Select Case subState
                    Case motionSubStateEnum.SETUP_AND_GO

                        '---------------------
                        'Support Model - 124X Only
                        'Hsien , 2015.01.26
                        '---------------------
                        'reset , sync option
                        __pointTable.ForEach(Sub(__point As cMotorPoint) AMaxM4_SetSyncOption(__point.AxisIndex, syncMode.IMMEDIATELY_START))
                        'resetSynchron()

                        If (__pointTable.Count > 1) Then
                            'there's other axis need to drive
                            __returnStatus = AMaxM4_SetSyncSignalMode(Me.MotorIndex, SynchonSignalMode)
                            If (__returnStatus <> 0) Then
                                Return statusEnum.EXECUTION_END_FAIL
                            End If
                        End If

                        '=== 同步啟動初始化 ======
                        For Each __point As cMotorPoint In __pointTable
                            With __point

                                '---------------------------
                                '   Speed setup
                                '---------------------------
                                If (.VelocityProfile = velocityProfileEnum.S_CURVE) Then
                                    __returnStatus = AMaxM4_SMovSet(.AxisIndex,
                                                                                                    .StartVelocity,
                                                                                                    .Velocity,
                                                                                                    .AccelerationTime,
                                                                                                    .DecelerationTime,
                                                                                                    .SShapeAccelerationTime,
                                                                                                    .SShapeDecelerationTime)
                                Else
                                    __returnStatus = AMaxM4_TMovSet(.AxisIndex,
                                                                                                   .StartVelocity,
                                                                                                   .Velocity,
                                                                                                   .AccelerationTime,
                                                                                                   .DecelerationTime)
                                End If

                                If (__returnStatus <> 0) Then
                                    Return __returnStatus
                                End If


                                'setup synchron parameters
                                If (.AxisIndex <> Me.MotorIndex) Then
                                    __returnStatus = AMaxM4_SetSyncSignalSource(.AxisIndex, Me.MotorIndex)
                                    If (__returnStatus <> 0) Then
                                        Return statusEnum.EXECUTION_END_FAIL
                                    End If
                                    __returnStatus = AMaxM4_SetSyncOption(.AxisIndex, SynchronMode)
                                    If (__returnStatus <> 0) Then
                                        Return statusEnum.EXECUTION_END_FAIL
                                    End If
                                    'slave axis motion setup
                                    If .PointType = pointTypeEnum.ABS Then  ' Abs-Move
                                        __returnStatus = AMaxM4_AMov(.AxisIndex, CInt(.Distance))
                                    Else ' Rel-Move
                                        __returnStatus = AMaxM4_RMov(.AxisIndex, CInt(.Distance))
                                    End If
                                Else
                                    '---------------
                                    '   My point
                                    '---------------
                                End If

                            End With
                        Next

                        '--------------------------------------------
                        '   Command content must be setted before
                        '--------------------------------------------
                        Dim __myPoint As cMotorPoint = __pointTable.Find(Function(obj As cMotorPoint) (obj.AxisIndex = Me.MotorIndex))

                        If (__myPoint.PointType = pointTypeEnum.ABS) Then
                            __returnStatus = AMaxM4_AMov(Me.MotorIndex, CInt(__myPoint.Distance))
                        Else
                            __returnStatus = AMaxM4_RMov(Me.MotorIndex, CInt(__myPoint.Distance))
                        End If

                        If (__returnStatus <> 0) Then
                            Return statusEnum.EXECUTION_END_FAIL
                        End If


                        examinationList.Clear()
                        examinationList.AddRange({AddressOf amaxWaitMotionStop,
                                                  AddressOf amaxWaitFinePosition})

                        handle = New motionStateObject
                        ThreadPool.QueueUserWorkItem(AddressOf waitMotionStopCommand, handle)

                        subState = motionSubStateEnum.WAITEXECUTION

                    Case motionSubStateEnum.WAITEXECUTION
                        If (handle.IsCompleted) Then
                            Return handle.result
                        End If
                End Select

                Return statusEnum.EXECUTING
            End Function

            Protected Function pauseCommand() As Integer
                'Hsien , 2014.09.14
                AMaxM4_PauseMotion(Me.MotorIndex)
                'timeOutTimer.IsEnabled = False  'pause the timeout timer , Hsien , 2016.02.17
                Return 0
            End Function
            Protected Function resumeCommand() As Integer
                AMaxM4_ResumeMotion(Me.MotorIndex)
                'timeOutTimer.IsEnabled = timeOutTimer.IsEnabled     ' reset time if had started
                Return 0
            End Function

            Protected Overridable Function resetCommand() As Integer
                ' reset motor into initial stage (wait command)
                __commandSubState = 0
                commandInExecute = motorCommandEnum.NONE
                'otherwise , next command may be skipped
                'Hsien  2014.07.14
                Return 0
            End Function

            Protected Function tableSetup() As returnErrorCodes
                '-----------------------------------------------
                '   Setup Simaltanous move settings
                ' Hsien , 2015.01.23
                '-----------------------------------------------
                Dim ____returnStatus As returnErrorCodes
                Dim axisArray As Integer() = New Integer(PointTable.Count - 1) {}
                Dim distanceArray As Integer() = New Integer(PointTable.Count - 1) {}
                Dim index As Integer = 0
                For Each __point As cMotorPoint In PointTable
                    With __point

                        If (.VelocityProfile = velocityProfileEnum.S_CURVE) Then
                            ____returnStatus = AMaxM4_SMovSet(.AxisIndex,
                                                                                    .StartVelocity,
                                                                                    .Velocity,
                                                                                    .AccelerationTime,
                                                                                    .DecelerationTime,
                                                                                    .SShapeAccelerationTime,
                                                                                    .SShapeDecelerationTime)
                        Else
                            ____returnStatus = AMaxM4_TMovSet(.AxisIndex,
                                                            .StartVelocity,
                                                            .Velocity,
                                                            .AccelerationTime,
                                                            .DecelerationTime)
                        End If


                        If (____returnStatus <> 0) Then
                            Return ____returnStatus
                        End If

                        axisArray(index) = .AxisIndex
                        distanceArray(index) = .Distance

                    End With

                    index += 1

                Next

                If (PointTable(0).PointType = pointTypeEnum.ABS) Then
                    ____returnStatus = AMaxM4_Set_A_Move_All(axisArray, distanceArray)
                Else
                    ____returnStatus = AMaxM4_Set_R_Move_All(axisArray, distanceArray)
                End If

                If (____returnStatus <> 0) Then
                    Return ____returnStatus
                End If

                '-----------------------------------------------
                '   Setup OK , wait for triggering
                ' Hsien , 2015.01.23
                '-----------------------------------------------
                Return returnErrorCodes.ERR_NoError
            End Function
#End Region

            Public Overrides Function raisingGUI() As Control
                Return New userControlMotor With {.Motor = Me,
                                                 .PropertyView = MyBase.raisingGUI}
            End Function

        End Class
    End Namespace


    Public Class motorControlDrivable
        Inherits motorControl
        Implements IDrivable
        'the IDrivable regular version of  motor control , Hsien , 2015.06.04

        Public ReadOnly Property CommandDrivenState As IDrivable.drivenState Implements IDrivable.CommandDrivenState
            Get
                If (CommandInExecute.Equals(motorControl.motorCommandEnum.WAIT_RECALL)) Then
                    Return IDrivable.drivenState.WAIT_RECALL
                ElseIf (CommandInExecute.Equals(motorControl.motorCommandEnum.NONE)) Then
                    Return IDrivable.drivenState.LISTENING
                Else
                    Return IDrivable.drivenState.EXECUTING
                End If
            End Get
        End Property

        Public Shadows ReadOnly Property CommandEndStatus As IDrivable.endStatus Implements IDrivable.CommandEndStatus
            Get
                'used interpreter to bridge
                Return __commandEndStatusInterpreter(MyBase.CommandEndStatus)
            End Get
        End Property

        Public Shadows ReadOnly Property CommandInExecute As Object Implements IDrivable.CommandInExecute
            Get
                Return MyBase.commandInExecute
            End Get
        End Property

        'Public Shadows Function drive(command As [Enum]) As IDrivable.endStatus Implements IDrivable.drive
        '    'used shadows to hide base method , Hsien , 2015.06.04
        '    Return __commandEndStatusInterpreter(MyBase.drive(command))
        'End Function
        Public Shadows Function drive(command As [Enum], Optional ByVal commandPosition As Object = Nothing) As IDrivable.endStatus Implements IDrivable.drive
            'used shadows to hide base method , Hsien , 2015.06.04
            Return __commandEndStatusInterpreter(MyBase.drive(command, commandPosition))
        End Function

        Public Function getCommands() As ICollection Implements IDrivable.getCommands
            Return CommandFunctionDictionary.Keys
        End Function

        Public Property TimeoutLimit1 As TimeSpan Implements IDrivable.TimeoutLimit 'use less


        'used to interpret command
        Dim __commandEndStatusInterpreter As Dictionary(Of motorControl.statusEnum, IDrivable.endStatus) = New Dictionary(Of motorControl.statusEnum, IDrivable.endStatus)

        Sub New()
            __commandEndStatusInterpreter.Add(statusEnum.EXECUTING, IDrivable.endStatus.EXECUTING)
            __commandEndStatusInterpreter.Add(statusEnum.EXECUTION_END, IDrivable.endStatus.EXECUTION_END)
            __commandEndStatusInterpreter.Add(statusEnum.EXECUTION_END_FAIL, IDrivable.endStatus.EXECUTION_END_FAIL)
        End Sub

        Public Overrides Function raisingGUI() As Control
            '----------------------------------------------------
            '   Hsien , 2015.02.05
            '----------------------------------------------------
            Dim uc As userControlDrivable = New userControlDrivable
            With uc
                .Component = Me
                .PropertyView = New userControlPropertyView With {.Drive = Me}
            End With
            Return uc
        End Function

    End Class


End Namespace
