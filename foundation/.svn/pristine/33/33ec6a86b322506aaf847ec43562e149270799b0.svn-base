Imports Automation
Imports Automation.Components.Services
Imports Automation.Components.CommandStateMachine
Imports Automation.Components

Public Enum LiftMotorUsedPositions
    MOTOR_HOME
    MOTOR_LOAD
    MOTOR_ACI_START
    MOTOR_ACI_SUB_SPACE
    MOTOR_ACI_SPACE
    MOTOR_ACI_BLANK
    MOTOR_JR_START
    MOTOR_JR_SUB_SPACE
    MOTOR_JR_SPACE
    MOTOR_JR_BLANK
    MOTOR_BACCINI_START
    MOTOR_BACCINI_SUB_SPACE
    MOTOR_BACCINI_SPACE
    MOTOR_MANZ_WAIT
    MOTOR_MANZ_APPROCH
    MOTOR_MANZ_START
    MOTOR_MANZ_SUB_SPACE
    MOTOR_MANZ_SPACE
    MOTOR_MANZ_SHELL_START
    MOTOR_P25X3_START_1
    MOTOR_P25X3_START_26
    MOTOR_P25X3_START_51
    MOTOR_P25X3_SUB_SPACE_1
    MOTOR_P25X3_SUB_SPACE_26
    MOTOR_P25X3_SUB_SPACE_51
    MOTOR_P25X3_SPACE
    MOTOR_UNLOAD
End Enum
Public Enum LiftShellMotorUsedPositions
    MOTOR_HOME
    MOTOR_MANZ_SHELL_LOAD
    MOTOR_MANZ_SHELL_START
    MOTOR_MANZ_SHELL_UNLOAD
End Enum
Public Enum CassetteStyle
    ACI
    ACI_CANADIAN
    JR
    BACCINI
    MANZ
    P25X3
End Enum
Public Enum FeedDir
    MOVE_IN
    MOVE_OUT
End Enum
Public Class CassetteLift : Inherits systemControlPrototype
    Implements IFinishableStation
    Public Property _FinishableFlag As New flagController(Of IFinishableStation.controlFlags) Implements IFinishableStation.FinishableFlags
    Public Property _UpstreamStation As List(Of IFinishableStation) Implements IFinishableStation.UpstreamStations

    Public liftFlags As flagController(Of flagsInLoaderUnloader)
    Dim tmr As singleTimer = New singleTimer With {.TimerGoal = New TimeSpan(0, 0, 5)}

    'Cassette:Cas    UD:Up Down    Position:Pos    Sensor:Sen
    'Hsien , for UD motor , use hardware slowdown to implement interlock protection , 2015.09.22
    Public UD_Motor As motorControl = New motorControl With {.IsEnabled = True}

    Public UD_Shell_Motor As motorControl = New motorControl With {.IsEnabled = True}

    Public UD_ConveyerMotor As IDrivable = New motorControlDrivable With {.IsEnabled = True} 'Shared , Hsien , 2015.06.04 , compatible with DC/SERVO


    Public UD_ConveyerSlowDownSen As sensorControl = New sensorControl 'With {.IsEnabled = True} '卡匣減速感測器
    Public UD_ConveyerReachSen As sensorControl = New sensorControl 'With {.IsEnabled = True} '卡匣到達感測器

    Friend ConveyerWaferEmpty As Func(Of Boolean) = Function() (True) '輸送帶設備給的升降卡匣的狀態

    Public cntWafer As Integer '硅片計數
    Public SetCasStyle As CassetteStyle

    Public SetWaferMoveDir As FeedDir '2015.10.24 jk: DO NOT SET IT OUTSIDE, just use Property WorkingType As workingTypeEnum to set them all

    Public ManzCasWaferSafeSen As New sensorControl With {.IsEnabled = False} '檢查硅片是否有完全進入內部卡匣(要為Off)

    Dim MotionFinished_f As Boolean

    Public GetBoxCounter As Func(Of Integer) = Function() (0) '得到clsCassetteTransport物件的工作料盒,針對P25X3型式的卡匣
    Public SetBoxCounter As Action = Sub() Console.WriteLine("") '設定clsCassetteTransport物件的工作料盒,如果為3則會載出卡匣
    Dim blnRaiseSpaceAgain As Boolean = False

    Public Function stateIgnite() As Integer
        If _FinishableFlag.viewFlag(IFinishableStation.controlFlags.COMMAND_IGNITE) = True Then
            _FinishableFlag.writeFlag(IFinishableStation.controlFlags.COMMAND_IGNITE, False)
            systemMainState = systemStatesEnum.EXECUTE
        End If
        Return 0
    End Function
    Public Function stateExecute() As Integer
        Dim CassetteWaferNum As Short
        Select Case systemSubState
            '-----------------------------------------------------
            '              硅片移出到輸送帶
            '-----------------------------------------------------
            Case 0 '檢查馬達是否在升降平台上
                If liftFlags.viewFlag(flagsInLoaderUnloader.CasOn_UD_ConveyerReady_f) Then
                    systemSubState = 20
                End If
            Case 20 '設定旗標
                liftFlags.writeFlag(flagsInLoaderUnloader.CasReadyWaferInOut_f, True) '可使極板放入旗標
                systemSubState = 30
            Case 30 '等待重置
                If Not liftFlags.viewFlag(flagsInLoaderUnloader.CasReadyWaferInOut_f) Then
                    cntWafer = cntWafer + 1   '計數料匣的位置
                    MotionFinished_f = False
                    systemSubState = 50
                Else '如果按下清卡匣鈕
                    If liftFlags.viewFlag(flagsInLoaderUnloader.CasCollect_f) Then '按下清卡匣鈕
                        '--------------------------------------------------------------------------------------
                        '在硅片載出卡匣情形下,此旗標設沒有作用,在硅片載入卡匣的情形下,此旗標設為True時,暫存區開始收料
                        liftFlags.writeFlag(flagsInLoaderUnloader.BufferCanStore_f, True) '使輸送帶開始儲料
                        '--------------------------------------------------------------------------------------
                        If ConveyerWaferEmpty() Then '清卡匣時要,檢查舌頭輸送帶上的兩個位置是否有硅片
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasReadyWaferInOut_f, False)
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasOn_UD_ConveyerReady_f, False)
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasUnloadEnable_f, True)
                            SetBoxCounter() '在此要把clsCassetteTransport的BoxCnt設為3,載出卡匣
                            cntWafer = 0
                            systemSubState = 0
                        End If
                    ElseIf _UpstreamStation IsNot Nothing Then '收料
                        If _UpstreamStation.Count > 0 AndAlso _UpstreamStation.TrueForAll(Function(upStation As IFinishableStation) (upStation.FinishableFlags.viewFlag(IFinishableStation.controlFlags.STATION_FINISHED))) Then '收料
                            liftFlags.writeFlag(flagsInLoaderUnloader.BufferCanStore_f, True) '使輸送帶開始儲料
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasReadyWaferInOut_f, False)
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasOn_UD_ConveyerReady_f, False)
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasUnloadEnable_f, True)
                            SetBoxCounter() '在此要把clsCassetteTransport的BoxCnt設為3,載出卡匣
                            cntWafer = 0
                            systemSubState = 0
                        End If
                    End If
                End If
            Case 50 '如果為Manz卡匣升降時必須檢查硅片是否有突出內層卡匣
                If SetCasStyle = CassetteStyle.MANZ AndAlso
                    ManzCasWaferSafeSen.IsSensorCovered AndAlso
                    SetWaferMoveDir = FeedDir.MOVE_IN Then
                    '-------------------------------------------------------
                    '   Further Check for manz cassette , Hsien , 2015.10.12
                    '-------------------------------------------------------
                    Dim ap As New alarmContentSensor
                    With ap
                        .Inputs = ManzCasWaferSafeSen.InputBit
                        .PossibleResponse = alarmContextBase.responseWays.RETRY
                        .Reason = alarmContentSensor.alarmReasonSensor.SHOULD_BE_OFF
                        .CallbackResponse(alarmContextBase.responseWays.RETRY) = Function() As Boolean
                                                                                     Return True
                                                                                 End Function
                        CentralAlarmObject.raisingAlarm(ap)
                    End With
                Else
                    systemSubState = 60
                End If
            Case 60
                UD_Motor.SlowdownEnable = enableEnum.DISABLE    'shut-off slowdown let UD_motor able to do pitch move , Hsien , 2015.10.12
                systemSubState = 70

            Case 70 '移動到的位置SubSpace,Space or Blank
                Dim PosIndex As LiftMotorUsedPositions
                PosIndex = DecideLifterPostion() '傳回判別後決定馬達要上升的位置點
                If UD_Motor.drive(motorControl.motorCommandEnum.GO_POSITION, PosIndex) = motorControl.statusEnum.EXECUTION_END Then
                    UD_Motor.SlowdownEnable = enableEnum.ENABLE     'resume protection , Hsien , 2015.10.12
                    MotionFinished_f = True '馬達升降完成
                End If

                If MotionFinished_f Then 'MotionFinished_f=True可以即時設定CasMove_UD_Ok_f=True

                    Select Case SetCasStyle '設定檢查硅片的個數
                        Case CassetteStyle.ACI, CassetteStyle.ACI_CANADIAN, CassetteStyle.JR
                            CassetteWaferNum = 100
                        Case CassetteStyle.BACCINI, CassetteStyle.MANZ
                            CassetteWaferNum = 50
                        Case CassetteStyle.P25X3
                            Dim NowBoxIndex As Integer
                            NowBoxIndex = GetBoxCounter()
                            CassetteWaferNum = 25 * NowBoxIndex
                    End Select

                    If cntWafer < CassetteWaferNum Then '檢查被移出的硅片的個數,未到達卡匣的片數

                        If SetCasStyle = CassetteStyle.P25X3 Then
                            If liftFlags.viewFlag(flagsInLoaderUnloader.IgnoreCasFirstWafer_f) Then '忽略每一層卡匣的第一片
                                If cntWafer = 1 Or cntWafer = 26 Or cntWafer = 51 Then
                                    cntWafer = cntWafer + 1   '計數料匣的位置
                                    MotionFinished_f = False
                                    Return 0
                                Else
                                    systemSubState = 20
                                End If
                            Else
                                systemSubState = 20
                            End If
                        ElseIf SetCasStyle = CassetteStyle.ACI_CANADIAN Then '阿特斯卡匣上升方式(Blank<未到逹硅片>+Space<到達硅片>)
                            If cntWafer = 51 And (Not blnRaiseSpaceAgain) Then
                                blnRaiseSpaceAgain = True '使卡匣再卡升一格(為了和40MW的升降動作一致)
                                MotionFinished_f = False
                                Return 0
                            Else
                                If blnRaiseSpaceAgain = True Then blnRaiseSpaceAgain = False
                                systemSubState = 20
                            End If
                        Else
                            'cassette other than P25X3 , ACI_CANADIAN
                            systemSubState = 20
                        End If

                    Else
                        '到達卡匣的片數
                        '所有的硅片已被移出/移入

                        If SetCasStyle = CassetteStyle.MANZ And SetWaferMoveDir = FeedDir.MOVE_OUT Then
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasCollect_f, True) '主要目的使舌頭輸送帶上的兩個位置沒有硅片
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasReadyWaferInOut_f, True) '使CassetteFeed可以繼續運作
                            systemSubState = 30
                        Else
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasOn_UD_ConveyerReady_f, False) '設定卡匣未備便
                            liftFlags.writeFlag(flagsInLoaderUnloader.CasUnloadEnable_f, True) '設定卡匣要被移出

                            'condition : (P25X3 and thrid box)  or Not the P25X3
                            '卡匣為第三料盒,硅片個數才能清為0,不然個會疊加
                            If ((SetCasStyle = CassetteStyle.P25X3) And (GetBoxCounter() = BoxSelect.P25X3_Box_3)) Or
                                (Not (SetCasStyle = CassetteStyle.P25X3)) Then
                                cntWafer = 0
                            End If

                            systemSubState = 0

                        End If

                    End If
                    liftFlags.writeFlag(flagsInLoaderUnloader.CasMove_UD_Ok_f, True) '使輸送帶可以移動
                Else
                    '--------------------------
                    '   Motion Not Ready
                    '--------------------------
                End If

        End Select

        Return 0

    End Function
    Function DecideLifterPostion() As LiftConveyerUsedPositions
        '判別後決定馬達要上升的位置點
        Dim PosIndex As LiftMotorUsedPositions

        Select Case SetWaferMoveDir '硅片流出/入卡匣方式選擇
            Case FeedDir.MOVE_OUT '硅片流出卡匣
                Select Case SetCasStyle '卡匣型式選擇
                    Case CassetteStyle.ACI, CassetteStyle.ACI_CANADIAN, CassetteStyle.JR '100層
                        If cntWafer = 1 Then 'PreSpaceDist
                            If SetCasStyle = CassetteStyle.ACI Or SetCasStyle = CassetteStyle.ACI_CANADIAN Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_SUB_SPACE
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_SUB_SPACE
                        ElseIf cntWafer = 51 Then 'BlankPos
                            If SetCasStyle = CassetteStyle.ACI_CANADIAN Then
                                If blnRaiseSpaceAgain Then '再上升一格
                                    PosIndex = LiftMotorUsedPositions.MOTOR_ACI_SPACE
                                Else
                                    PosIndex = LiftMotorUsedPositions.MOTOR_ACI_BLANK
                                End If
                            End If
                            If SetCasStyle = CassetteStyle.ACI Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_BLANK
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_BLANK
                        Else '<=49 or >=51'SpacePos
                            If SetCasStyle = CassetteStyle.ACI Or SetCasStyle = CassetteStyle.ACI_CANADIAN Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_SPACE
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_SPACE
                        End If
                    Case CassetteStyle.BACCINI, CassetteStyle.MANZ '50層
                        If cntWafer = 1 Then 'PreSpaceDist
                            If SetCasStyle = CassetteStyle.BACCINI Then PosIndex = LiftMotorUsedPositions.MOTOR_BACCINI_SUB_SPACE
                            If SetCasStyle = CassetteStyle.MANZ Then PosIndex = LiftMotorUsedPositions.MOTOR_MANZ_SUB_SPACE
                        Else '<=50 SpacePos
                            If SetCasStyle = CassetteStyle.BACCINI Then PosIndex = LiftMotorUsedPositions.MOTOR_BACCINI_SPACE
                            If SetCasStyle = CassetteStyle.MANZ Then PosIndex = LiftMotorUsedPositions.MOTOR_MANZ_SPACE
                        End If
                    Case CassetteStyle.P25X3 '75層
                        If cntWafer = 1 Then 'PreSpaceDist1
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_1
                        ElseIf cntWafer = 26 Then 'PreSpaceDist26
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_26
                        ElseIf cntWafer = 51 Then 'PreSpaceDist51
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_51
                        Else '<=75 SpacePos
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SPACE
                        End If
                End Select
            Case FeedDir.MOVE_IN '硅片流入卡匣
                Select Case SetCasStyle '卡匣型式選擇
                    Case CassetteStyle.ACI, CassetteStyle.JR
                        If cntWafer <= 49 Or (cntWafer > 50 And cntWafer < 100) Then  '<=49 or >50 'SpacePos
                            If SetCasStyle = CassetteStyle.ACI Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_SPACE
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_SPACE
                        ElseIf cntWafer = 50 Then 'BlankPos
                            If SetCasStyle = CassetteStyle.ACI Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_BLANK
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_BLANK
                        ElseIf cntWafer = 100 Then 'PreSpacePos
                            If SetCasStyle = CassetteStyle.ACI Then PosIndex = LiftMotorUsedPositions.MOTOR_ACI_SUB_SPACE
                            If SetCasStyle = CassetteStyle.JR Then PosIndex = LiftMotorUsedPositions.MOTOR_JR_SUB_SPACE
                        End If
                    Case CassetteStyle.BACCINI, CassetteStyle.MANZ
                        If cntWafer = 50 Then 'PreSpaceDist
                            If SetCasStyle = CassetteStyle.BACCINI Then PosIndex = LiftMotorUsedPositions.MOTOR_BACCINI_SUB_SPACE
                            If SetCasStyle = CassetteStyle.MANZ Then PosIndex = LiftMotorUsedPositions.MOTOR_MANZ_SUB_SPACE
                        Else '<=50 SpacePos
                            If SetCasStyle = CassetteStyle.BACCINI Then PosIndex = LiftMotorUsedPositions.MOTOR_BACCINI_SPACE
                            If SetCasStyle = CassetteStyle.MANZ Then PosIndex = LiftMotorUsedPositions.MOTOR_MANZ_SPACE
                        End If
                    Case CassetteStyle.P25X3
                        If cntWafer = 25 Then 'PreSpaceDist26
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_51
                        ElseIf cntWafer = 50 Then 'PreSpaceDist51
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_26
                        ElseIf cntWafer = 75 Then 'PreSpaceDist1
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SUB_SPACE_1
                        Else '<=75 SpacePos
                            PosIndex = LiftMotorUsedPositions.MOTOR_P25X3_SPACE
                        End If
                End Select
        End Select

        Return PosIndex

    End Function
    Sub New()
        '將自定義起始化函式加入 通用起始化引動清單
        Me.initialize = [Delegate].Combine(Me.initialize, New Func(Of Integer)(AddressOf initMappingAndSetup))
    End Sub

    Function initMappingAndSetup() As Integer
        relatedFlags.AddRange(liftFlags.FlagElementsArray)
        '本站主狀態函式設定
        systemMainStateFunctions(systemStatesEnum.IGNITE) = AddressOf stateIgnite       '鍊結主狀態函式
        systemMainStateFunctions(systemStatesEnum.EXECUTE) = AddressOf stateExecute     '鍊結主狀態函式
        systemMainState = systemStatesEnum.IGNITE   '設定初始主狀態
        'initEnableAllDrives()

        'Hsien , 2015.10.12 , for lifter motor , use slow-down to do whid-wide protection
        With UD_Motor
            .SlowdownEnable = enableEnum.ENABLE 'enabled since start
            .SlowdownLatch = sdLatchEnum.LATCH
            .SlowdownMode = sdModeEnum.SLOW_DOWN_STOP
            .alarmPackage.PossibleResponse = .alarmPackage.PossibleResponse Or alarmContextBase.responseWays.RETRY  'possible to retry
        End With

        Return 0
    End Function


    Sub lifterPauseHandler() Handles PauseBlock.InterceptedEvent, CentralAlarmObject.alarmOccured
        '升降暫停
        UD_Motor.drive(motorControl.motorCommandEnum.MOTION_PAUSE)
    End Sub
    Sub lifterUnpauseHandler() Handles PauseBlock.UninterceptedEvent, CentralAlarmObject.alarmReleased
        '升降恢復
        UD_Motor.drive(motorControl.motorCommandEnum.MOTION_RESUME)
    End Sub

    '-----------------------------------------
    'Hsien , 2015.10.12 , the local translator
    '-----------------------------------------
    Sub translateLifterMotorAlarm(sender As alarmManager, e As EventArgs) Handles CentralAlarmObject.alarmOccured
        If (UD_Motor.IsMyAlarmCurrent AndAlso
            UD_Motor.ErrorStatus = errorStatusEnum.STOPPED_SD_ON) Then
            sender.CurrentAlarm.AdditionalInfo = "升降平台動作干涉檢知，請確認舌頭是否縮回、或卡匣輸送帶交接處有無異物"
        End If
    End Sub


End Class

